; PAGE 1 FOLLOWS



	TITLE	MISSILE	COMMAND
	; LIST	NOCOND,NOGEN,OFF
        LIST NOCOND,NOGEN
LSTAT	MACRO
	LIST	ON
; There's no LIST OFF before page 26
	MEND
	LSTAT
	NAME	MISSIL
TORG	EQU	1100H
DOT	MACRO
	RST	38H
	MEND
CDOS	MACRO	#1
	LD	DE,MISFCB
	LD	C,#1
	CALL	5
	MEND
GET	MACRO	#1
	LD	A,0
#1	EQU	$-1
	MEND
INCM    MACRO   #1
        LD      A,#1
        INC     A
        LD      #1,A
        MEND
LOOPB   MACRO   #1,#2
        MOV     (#1),0
        LD      A,0
#1      EQU     $-1
#1L     CP      #2
        JP      Z,#1X
        MEND
ENDLPB  MACRO   #1
        INCM    (#1)
        JP      #1L
#1X     EQU     $
        MEND
ORMEM   MACRO   #1
        LD      A,(IX+#1)
        OR      (HL)
        LD      (HL),A
        MEND
ANDMEM  MACRO   #1
        LD      A,(IX+#1)
        CPL
        AND     (HL)
        LD      (HL),A
        MEND
BLTBYT  MACRO   #1,#2
        #1      #2
        INC     HL
        LD      A,15
        AND     L
        JR      NZ,$+6 ; Crossed over to a right quadrant
        LD      DE,2*256-16
        ADD     HL,DE
        MEND
BLTLIN  MACRO   #1,#2
        BIT     6,C
        JR      Z,$+4
        SET     2,H
        RES     1,H
        BIT     6,B
        JR      Z,$+4
        SET     1,H
        BLTBYT  #1,#2
        BLTBYT  #1,#2+1
        #1      #2+2
        INC     C
        INC     C
        LD      DE,16-2
        ADD     HL,DE
        LD      A,15
        AND     L
        CP      14      ; 14,15 are in a left quadrant
        JR      C,$+6
        LD      DE,0FE10H ; -2, 16
        ADD     HL,DE
        MEND
BLT     MACRO   #1
        BLTLIN  #1,0
        BLTLIN  #1,3
        BLTLIN  #1,6
        BLTLIN  #1,9
        MEND
        
; arrow bit blit data
AR1	MACRO
	DB	7,79,12,128,224,32
	MEND
AR2	MACRO
	DB	27,223,76,0,128,0
	MEND
AR3	MACRO
	DB	1,13,4,192,248,176
	MEND
SHIFT	MACRO	#4,#5
	R#4CA
SHIF1	DL	#5-1
	IF	SHIF1	NE	0
	R#4CA
SHIF1	DL	SHIF1-1
	ENDIF
	IF	SHIF1	NE	0
	R#4CA
SHIF1	DL	SHIF1-1
	ENDIF
	IF	SHIF1	NE	0
	R#4CA
	ENDIF
	MEND
MOV	MACRO	#6,#7
	LD	A,#7
	LD	#6,A
	MEND
CMP	MACRO	#8,#9
	LD	A,#9
	CP	#8
	MEND
; Tuart port definitions
TUARTA  EQU     0
TUARTB  EQU     32        
TIMER1  EQU     5 
TIMER2  EQU     6
TIMER3  EQU     7
TIMER4  EQU     8
TIMER5  EQU     9
IMASKP  EQU     3
ICOMMP  EQU     2  
	ORG	TORG-14
SHIFT	LD	DE,TORG+PROG-DAZTAB-1
	LD	HL,10EH+PROG-DAZTAB-1
	LD	BC,PROG-DAZTAB
	LDDR
	JP	START
DAZTAB	DB	1,4,2,8,16,64,32,128
XY	EQU	38H
; PAGE 2 FOLLOWS
YX	PUSH	BC
	EXX
	POP	BC
	LD	H,SC1/512
	BIT	6,B
	JR	Z,XY1
	INC	H
XY1	BIT	6,C
	JR	Z,XY2
	INC	H
	INC	H
XY2	LD	A,3CH
	AND	B
	SHIFT	R,2
	LD	L,A
	LD	A,3EH
	AND	C
	SHIFT	L,2
	ADD	A,A
	RL	H
	OR	L
	LD	L,A
	LD	A,3
	AND	B
	RRC	C
	RLA
	LD	C,A
	LD	B,DAZTAB/256
XYACMP	EQU	$-YX+38H
	LD	A,(BC)
	JP	YX
XYLEN	EQU	$-YX
ONOFF	EQU	$-2-YX+38H
XYP	MACRO	#1
	#1	(HL)
	LD	(HL),A
	SET	3,H
	MEND
XYL	MACRO	#2
	#2	(HL)
	LD	(HL),A
	EXX
	RET
	MEND
C00	EQU	$-[$/256*256]
	CPL
	XYP	AND
	LD	A,(BC)
	CPL
	XYL	AND
C01	EQU	$-[$/256*256]
	XYP	OR
	LD	A,(BC)
	CPL
	XYL	AND
C10	EQU	$-[$/256*256]
	CPL
	XYP	AND
	LD	A,(BC)
	XYL	OR
C11	EQU	$-[$/256*256]
	XYP	OR
	LD	A,(BC)
	XYL	OR
CTEST	EQU	$-[$/256*256]
	AND	(HL)
; PAGE 3 FOLLOWS
	LD	D,1
	JR	NZ,CTST1
	LD	D,0
CTST1	SET	3,H
	LD	A,(BC)
	AND	(HL)
	LD	A,D
	JR	Z,CTST2
	OR	2
CTST2	EXX
	RET
PUT	MACRO	#3
	LD	A,#3
	CALL	PNUM
	MEND
        ORG     ($/256+1)*256
DAZTB1  DB      1, 1 SHL 1, 1 SHL 4, 1 SHL 5, 1 SHL 2, 1 SHL 3, 1 SHL 6, 1 SHL 7 ; Official Dazzler memory layout (x+y*4)  
; This version of XY only plots in first quadrant in one color
XY16    LD      HL,SC1
        LD      A,3EH
        AND     C
        ADD     A,A
        ADD     A,A
        LD      E,A
        LD      D,0
        ADD     HL,DE
        ADD     HL,DE
        LD      A,B
        SRL     A
        SRL     A
        LD      E,A
        ADD     HL,DE
        LD      A,3
        AND     B
        BIT     0,C
        JR      Z,XY16A
        ADD     A,4
XY16A   LD      D,DAZTB1/256
        LD      E,A
        LD      A,(DE)
        OR      (HL)
        LD      (HL),A
        RET
        
; Clear first character on screen plus bit blit dimensions        
CLSZ    LD      DE,16
        LD      B,4
        LD      IX,SC1
CLSZ1   LD      (IX),0
        LD      (IX+1),0
        LD      (IX+2),0
        ADD     IX,DE
        DJNZ    CLSZ1
        RET
      
CLEAR   LD      HL,SC1
        LD      DE,SC1+1
        LD      BC,4095
        LD      (HL),0
        LDIR
        RET
        
INTRAB	DW	INT1,INT2,START,WHSI,KEYI,KEYO,OMI,MLOWI
	DW	ESCAPE,START,START,START,START,START,START,START
START	IN	A,18H
	AND	8
	JR	NZ,NOTST
	LD	HL,SC1
CTSTA	LD	A,L
	RRCA
	AND	7
	OR	8
	LD	B,A
	SHIFT	L,4
	OR	B
	LD	(HL),A
	INC	HL
	LD	A,[SC1+512]/256
	CP	H
	JR	NZ,CTSTA
	LD	DE,SC1+28*16
	LD	HL,ADAT
	LD	BC,64
	LDIR
	LD	A,SC1/512+128
	OUT	14,A
	LD	A,16
	OUT	15,A
CTSTB	IN	A,18H
	AND	8
	JR	NZ,NOTST
	JR	CTSTB
; Test pattern graphics
ADAT	DB	-1,15,7,7,0,9,0E0H,0,160,0,0D0H,0,0BBH,0,12,192
	DB	-16,0,7,7,90H,90H,0E0H,0,0A0H,0,0D0H,0B0H,0,0
	DB	0CCH,0C0H
	DB	-16,0,7,7,90H,99H,0E0H,0,0A0H,0,0D0H,0B0H,0B0H
	DB	11,12,0CCH
	DB	-16,0,70H,0,90H,90H,0E0H,0EEH,0E0H,0EEH,0D0H
	DB	0,0BBH,0,12,0C0H
NOTST	CALL	CLEAR
	CALL	DISKR
	DI
	LD	A,9
	OUT	TUARTA+ICOMMP,A
	OUT	TUARTB+ICOMMP,A
; PAGE 4 FOLLOWS
        XOR     A               ; Temporary hack until interrupts debugged
	; LD	A,0CBH                  ; Tuart A Interrupt Mask
	OUT	TUARTA+IMASKP,A
	LD	A,1
	OUT	TUARTB+IMASKP,A      ; Tuart B Interrupt Mask
	MOV	I,IIIIII/256
	LD	HL,INTRAB
        LD      DE,IIIIII
	LD	BC,32
	LDIR
	LD	HL,YX
	LD	DE,38H
	LD	BC,XYLEN
	LDIR
	IM	2
	EI
	LD	A,248
	OUT	TUARTA+TIMER1,A
	LD	A,-1
	OUT	TUARTA+TIMER4,A
	OUT	TUARTB+TIMER1,A
	XOR	A
; Unknown port used in 1982 machine
;	OUT	34H,A
	LD	(SMISCT),A
	DEC	A
	LD	(SPACE+3),A
	LD	HL,PLAY1
	LD	B,36
SCRCLR	LD	(HL),0
	INC	HL
	DJNZ	SCRCLR
	JP	GBEGIN
HIGHST	LD	HL,HIGHS
	LD	B,8
	LD	D,95H
CLHIGH	LD	C,3
CHIGH1	CALL	RAND
	CP	25
	JR	NZ,CHIGH1
	ADD	A,12
        LD      (HL),A
	INC	HL
	DEC	C
	JR	NZ,CHIGH1
	LD	C,3
CHIGH2	LD	(HL),0
	INC	HL
	DEC	C
	JR	NZ,CHIGH2
	LD	(HL),7
	INC	HL
	LD	(HL),5
	LD	A,D
	SUB	5
	DAA
	LD	D,A
	AND	0F0H
	SHIFT	R,4
	INC	HL
	LD	(HL),A
	LD	A,0FH
	AND	D
	INC	HL
	LD	(HL),A
	INC	HL
	DJNZ	CLHIGH
; PAGE 5
	RET
; This is a bad idea, but currently no Tuart B interrupts anyway 12/09/2020
ESCAPE	DI
	PUSH	AF
	IN	A,1
	CP	1BH
	JR	NZ,ESCAP1
	XOR	A
	OUT	TUARTA+IMASKP,A
	OUT	TUARTB+IMASKP,A
	OUT	14,A
	JP	0
ESCAP1	LD	A,-1
	OUT	TUARTB+TIMER1,A
	POP	AF
	EI
	RET
HIGHSC	MOV	(HW1),0
	LD	HL,PLAY1+8
	LD	DE,PLAY2+8
	CALL	COMP
	LD	DE,PLAY2+8
	LD	A,2
	JR	NC,KHKH
	LD	DE,PLAY1+8
	LD	A,1
KHKH	LD	(HC),A
	CALL	HIGHC
	LD	DE,PLAY1+8
	LD	A,(HC)
	DEC	A
	JR	NZ,DSDS
	LD	DE,PLAY2+8
DSDS	GET	HC
	XOR	3
	LD	(HC),A
	CALL	HIGHC
	GET	HW1
	OR	A
	JP	Z,END
	JP	GBEGIN
COMP	LD	B,8
COMP2	DJNZ	COMP1
COMP3	OR	A
	RET
COMP1	LD	A,(DE)
	CP      (HL)
	RET	C
	INC	DE
	INC	HL
	JR	Z,COMP2
	JR	COMP3
HIGHC	LD	(SPR),DE
	LD	A,8
	LD	(HW2),A
	LD	HL,HIGHS+3
HIGHC1	PUSH	HL
	LD	DE,0
SPR	EQU	$-2
	CALL	COMP
	POP	HL
	JR	NC,HIGHC9
	LD	BC,10
	ADD	HL,BC
	GET	HW2
	DEC	A
	LD	(HW2),A
; PAGE 6 FOLLOWS
	JR	NZ,HIGHC1
	RET
HIGHC9	LD	A,(HW2)
	DEC	A
	LD	HL,HIGHS+70
	JR	Z,HIGHCF
	DEC	HL
	LD	DE,HIGHS+79
HIGHC3	LD	BC,10
	LDDR
	DEC	A
	JR	NZ,HIGHC3
	INC	HL
HIGHCF	LD	A,(HW1)
	OR	A
	JR	NZ,HIGHR4
	LD	C,5
	MOV	(ONOFF),(BG)
XYCA	LD	B,127
XYCA1	DOT
	DJNZ	XYCA1
	DOT
	INC	C
	CMP	C,96
	JR	NZ,XYCA
	MOV	(ONOFF),(FM)
	PUSH	HL
	LD	HL,AREADY+10
	LD	BC,16+41*256
	CALL	MESOUT
	LD	HL,HIMES
	LD	BC,26+5*256
	CALL	MESOUT
	LD	BC,38+5*256
	CALL	MESOUT
	LD	BC,44+5*256
	CALL	MESOUT
	LD	BC,50+5*256
	CALL	MESOUT
	LD	BC,56+5*255
	CALL	MESOUT
	LD	A,1
	LD	(HW1),A
	POP	HL
HIGHR4	LD	A,(HC)
	XOR	3
	LD	D,A
	LD	BC,16+83*256
	MOV	(ONOFF),(BG)
	CALL	LOUT
	LD	A,(HC)
	LD	D,A
	MOV	(ONOFF),(EM)
	CALL	LOUT
ASQWE	MOV	(ONOFF),(BG)
	LD	C,51
XYC3	LD	B,89
XYC4	DOT
	INC	B
	CMP	B,110
	JR	NZ,XYC4
	INC	C
	CMP	C,58
	JR	NZ,XYC3
	LD	BC,57+89*256
	LD	A,3
; PAGE 7 FOLLOWS
	LD	(CCNT),A
NAME1	LD	A,B
	ADD	A,6
	LD	B,A
	LD	D,11
	MOV	(ONOFF),(EX)
	CALL	LOUT
	LD	A,1
	LD	(KAQW),A
NAME2	IN	A,18H
	CPL
	AND	7
	JR	NZ,ENTER
	LD	(KAQW),A
	IN	A,18H
	AND	8
	JR	NZ,KAQW1
ASDFE	LD	A,(CCNT)
	CP	3
	JR	Z,ASQWE
	DEC	HL
	LD	A,(CCNT)
	INC	A
	LD	(CCNT),A
	JR	ASDFE
KAQW1	IN	A,19H
	LD	E,A
	IN	A,1AH
	OR	E
	JP	M,NAME9
	CP	64
	JR	C,NAME2
	MOV	(ONOFF),(BG)
	CALL	LOUT
	JR	NAME3
NAME9	NEG
	CP	64
	JR	C,NAME2
	MOV	(ONOFF),(BG)	
	CALL	LOUT
	DEC	D
	DEC	D
NAME3	INC	D
	LD	A,D
	CP	9
	JR	NZ,NAME4
	LD	A,36
NAME4	CP	37
	JR	NZ,NAME5
	LD	A,10
NAME5	LD	D,A
	MOV	(ONOFF),(EX)
	CALL	LOUT
	PUSH	HL
	LD	HL,4000H
	CALL	DELAY
	POP	HL
	JR	NAME2
ENTER	GET	KAQW
	OR	A
	JP	NZ,KAQW1
	INC	A
	LD	(KAQW),A
	LD	(HL),D
	INC	HL
	GET	CCNT
; PAGE 8 BELOW
	DEC	A
	LD	(CCNT),A
	JP	NZ,NAME1
	LD	DE,(SPR)
	EX	DE,HL
	LD	BC,7
	LDIR
	RET
LOUT	PUSH	HL
	PUSH	DE
	PUSH	BC
	PUT	D
	POP	BC
	POP	DE
	POP	HL
	RET
 ; CONGRATULATIONS  YOU HAVE ACHIEVE A TRULY INCREDIBLE SCORE PLEASE ENTER YOU INITIALS
HIMES	DB	45,45,10,13,25,24,17,28,11,30,31,22,11,30,19
	DB	25,24,29,10,45,45,255,10,35,25,31,10,18,11
	DB	32,15,10,11,13,18,19,15,32,15,14,10,11,255
	DB	10,10,30,28,31,22,35,10,19,24,13,28,15,14,19,12
	DB	22,15,255,10,29,13,25,28,15,43,10,26,22,15,11
	DB	29,15,10,15,24,30,15,28,255,35,25,31,28,10,19
	DB	24,19,30,19,11,22,29,46,255
DAZMOD	PUSH AF
        LD	A,SC1/512+80H
	LD	(DZOFF1),A
	LD	A,SC2/512+80H
	LD	(DZOFF2),A
	LD	A,(COL1)
	OUT	15,A
	LD	A,(COL2)
	OUT	15,A
	LD	A,0D1H
	OUT	15,A
	LD	A,(DZOFF1)
	OUT	14,A
        POP     AF
	RET
GBEGIN	CALL	CLEAR
	LD	A,(HW1)
	AND	A
	JR	Z,GBGIN
	MOV	(DZOFF1),A
	LD	(DZOFF2),A
	DI
	CALL	DISKW
	LD	A,SC1/512+80H
	LD	(DZOFF1),A
	LD	A,SC2/512+80H
	LD	(DZOFF2),A
	EI
	LD	A,-1
	OUT	TUARTA+TIMER4,A
	OUT	TUARTA+TIMER5,A
	OUT	TUARTA+TIMER1,A
GBGIN	MOV	(COL1),(COLTAB)
	MOV	(COL2),(COLTAB+1)
	CALL	DAZMOD
        MOV     (PACEDZ),30     ; Overcome OS craziness with shutting off Dazzler by resetting 2x/sec
	IN	A,18H
	AND	1
	JP	NZ,NUMCIT
	LD	HL,BCITY
	LD	BC,70+4*256
	MOV	(ONOFF),C10
	CALL	MESOUT
	MOV	(ONOFF),C01
	LD	HL,BCITY8
	LD	BC,70+94*256
	CALL	MESOUT
	LD	H,0
	LD	L,8
BCITY5	LD	A,H
	SHIFT	L,4
	OR	L
	LD	(BONUS),A
	MOV	(ONOFF),C01
	LD	BC,70+94*256
	PUSH	HL
	LD	A,H
	AND	A
	JR	NZ,BCITY1
	LD	A,10
; PAGE 9 FOLLOWS
BCITY1	CALL	PNUM
	POP	HL
	PUSH	HL
	PUT	L
	POP	HL
BCITY4	IN	A,18H
	AND	1
	JP	NZ,NUMCIT
	PUSH	HL
	LD	HL,4000H
	CALL	DELAY
	POP	HL
	IN	A,1AH
	AND	A
	JP	M,BCITY3
	ADD	A,-64
	JP	M,BCITY4
	CALL	CLNUM
	INC	L
	CMP	L,10
	JR	NZ,BCITY5
	LD	L,0
	INC	H
	CMP	H,10
	JR	NZ,BCITY5
	LD	H,9
	LD	L,9
	JR	BCITY5
BCITY3	NEG
	ADD	A,-64
	JP	M,BCITY4
	CALL	CLNUM
	DEC	L
	JP	P,BCITY6
	LD	L,9
	DEC	H
	JR	BCITY5
BCITY6	JR	NZ,BCITY5
	LD	A,H
	AND	A
	JR	NZ,BCITY5
	LD	L,1
	JR	BCITY5
CLNUM	LD	BC,70+94*256
	PUSH	HL
	MOV	(ONOFF),C00
	LD	A,H
	AND	A
	JR	NZ,CLNUM1
	LD	A,10
CLNUM1	CALL	PNUM
	POP	HL
	PUSH	HL
	PUT	L
	POP	HL
	RET
NUMCIT	IN	A,18H
	AND	2
	JP	NZ,START1
	CALL	CLEAR
	LD	HL,SC1
NCIT1	LD	(HL),255
	INC	HL
	CMP	H,[SC1+800H]/256
	JR	NZ,NCIT1
	LD	BC,70+5*256
; PAGE 10 FOLLOWS
	LD	HL,NCITY2
	MOV	(ONOFF),C00
	CALL	MESOUT
	MOV	(ONOFF),C11
	LD	H,4
	LD	BC,70+102*256
	PUSH	HL
	PUT	H
	POP	HL
NCIT3	MOV	(SCITY),H
	IN	A,18H
	AND	2
	JR	NZ,START1
        LD      DE,0
NCIT4	DEC	DE
	LD	A,E
	OR	D
	JR	NZ,NCIT4
	MOV	(ONOFF),C01
	LD	BC,70+102*256
	PUSH	HL
	PUT	H
	POP	HL
	IN	A,1AH
	AND	A
	JP	M,NCIT2
	ADD	A,-64
	JP	M,WRITN
	INC	H
	LD	A,H
	ADD	A,-7
	JP	M,WRITN
	LD	H,6
	JR	WRITN
NCIT2	NEG
	ADD	A,-64
	JP	M,WRITN
	DEC	H
	JR	NZ,WRITN
	LD	H,1
WRITN	MOV	(ONOFF),C11
	PUSH	HL
	LD	BC,70+102*256
	PUT	H
	POP	HL
	JP	NCIT3
NCITY2	DB	24,31,23,12,15,28,10,25,16,10,13,19,30,19,15 ; NUMBER OF CITIES
	DB	29,255
START1	CALL	CLEAR
	LD	HL,100H
RSET	CALL	RAND
	LD	(HL),A
	INC	L
	JR	NZ,RSET
	MOV	(ONOFF),C10
	CALL	SCOR
	JR	SCORA
SCOR	LD	BC,4+4*256
	LD	HL,PLAY1+7
	CALL	SCOR4
	LD	BC,4+44*256
	LD	HL,HIGHS+2
	CALL	SCOR4
	LD	A,(PLAY)
	DEC	A
	RET	Z
; PAGE 11 FOLLOWS
	LD	BC,4+87*256
	LD	HL,PLAY2+7
	JR	SCOR4
SCORA	LD	HL,HIGHS
	MOV	(ONOFF),C11
	LD	D,8
	LD	BC,16+35*256
SCOR5	LD	E,3
SCOR7	PUSH	HL
	PUSH	DE
	PUT	(HL)
	POP	DE
	POP	HL
	INC	HL
	DEC	E
	JR	NZ,SCOR7
	LD	A,6
	ADD	A,B
	LD	B,A
	JR	SCOR6
SCOR4	LD	D,6
SCOR1	DEC	D
	INC	HL
	JP	M,SCOR2
	LD	A,(HL)
	AND	A
	JR	NZ,SCOR2
	LD	A,6
	ADD	A,B
	LD	B,A
	JR	SCOR1
SCOR2	INC	D
SCOR3	PUSH	HL
	PUSH	DE
	PUT	(HL)
	POP	DE
	POP	HL
	INC	HL
	DEC	D
	JP	P,SCOR3
	RET
SCOR6	PUSH	DE
	DEC	HL
	CALL	SCOR4
	POP	DE
	LD	A,6
	ADD	A,C
	LD	C,A
	LD	B,35
	DEC	D
	JR	NZ,SCOR5
SCOR8	LD	BC,70+4*256
	LD	HL,BCITY
	MOV	(ONOFF),C10
	CALL	MESOUT
	LD	A,6
	ADD	A,B
	LD	B,A
	MOV	(ONOFF),C01
	LD	A,(BONUS)
	AND	0F0H
	LD	A,10
	JR	Z,SCOR9
	LD	A,(BONUS)
	AND	0F0H
	SHIFT	R,4
; PAGE 12 FOLLOWS
SCOR9	CALL	PNUM
	LD	A,(BONUS)
	AND	15
	CALL	PNUM
	PUT	0
	PUT	0
	PUT	0
	LD	A,C01
	LD	(ONOFF),A
	CALL	FORE
	MOV	(CITPU1),(SCITY)
	LD	HL,CITPOS
CITPUT	LD	A,5		; Draw cities
	ADD	(HL)
	LD	B,A
	INC	HL
	LD	C,(HL)
	PUSH	HL
	PUSH	BC
	MOV	(ONOFF),C11
	PUT	39		; City graphics are special characters
	PUT	40
	POP	BC
	MOV	(ONOFF),C10	; Friendly color
	PUT	41
	PUT	42
	POP	HL
	INC	HL
	GET	CITPU1
	DEC	A
	LD	(CITPU1),A
	JR	NZ,CITPUT
	MOV	(ONOFF),C10
	CALL	MISPO
	JR	PWIPE
MISPO	LD	HL,MISPOS	; Display base missiles
	MOV	(MISPU1),3
MISPUT	GET     MISPU1
	DEC	A
	LD	(MISPU1),A
	RET	M
	LD	DE,MISDIS
	LD	B,(HL)
	INC	HL
	LD	C,(HL)
	INC	HL
	MOV	(MISPU3),10
MISPU4	GET	MISPU3
	DEC	A
	LD	(MISPU3),A
	JP	M,MISPUT
	LD	A,(DE)
	INC	DE
	ADD	A,B
	LD	B,A
	LD	A,(DE)
	INC	DE
	ADD	A,C
	LD	C,A
	CALL	LISSIM
	JR	MISPU4
MOVERZ	PUSH	HL
	EXX
	POP	HL
	LD	B,16
MVERZ1	RRD
; PAGE 13 FOLLOWS
	DEC	HL
	DJNZ	MVERZ1
	EXX
	RET
PWIPE	LD	HL,PLAY1
	LD	DE,PLAY1+1
	LD	BC,35
	LD	(HL),0
	LDIR
	MOV	(PLAY),1
	LD	HL,DFCTY
	LD	BC,25*256+96
	MOV	(ONOFF),C10
	CALL	MESOUT
DISPL	MOV	(DISP),0
	LD	(BMESP),A
DIS6	PUSH	DE              ; flash the arrows
	GET	ARFLSH
	INC	A
	CP	16
	JR	NZ,AR1
	CALL	TAKEAR
	XOR	A
AR1	LD	(ARFLSH),A
	CP	0
	JR	NZ,AR2
	CALL	PUTAR
AR2	POP	DE
	LD	HL,0C00H
	CALL	DELAY
;	FOOEY
	MOV	(DIS2A),-1
	LD	HL,SC1+2047
DIS3	LD	B,3
DIS2	GET	DIS2A
	CALL	MOVERZ
	LD	DE,-512
	ADD	HL,DE
	CALL	MOVERZ
	LD	DE,496
	ADD	HL,DE
	DJNZ	DIS2
	LD	HL,SC2+2047
	LD	A,(DIS2A)
	INC	A
	LD	(DIS2A),A
	JR	Z,DIS3
	GET	DISP
	INC	A
	LD	(DISP),A
	CP	3
	JR	NZ,DIS6
	MOV	(DISP),0
	LD	BC,122*256+127
	GET	BMESP
	PUSH	AF
	LD	E,A
	LD	D,0
	LD	HL,INMES
	ADD	HL,DE
	MOV	(ONOFF),C00
	PUT	(HL)
	POP	AF
	INC	A
	CP	BMESPL
	JR	NZ,DIS4
; PAGE 14 FOLLOWS
	XOR	A
DIS4	LD	(BMESP),A
	IN	A,18H
	AND	4               ; Button from joystick
	JR	Z,ONE
	IN	A,18H
	AND	8       
	JR	NZ,DIS6
TWO	MOV	(PLAY2+15),(BONUS)
	LD	(PLAY2+16),A
	MOV	(PLAY2),(SCITY)
	LD	B,A
	LD	HL,PLAY2+2
TWO1	LD	(HL),1
	INC	HL
	DJNZ	TWO1
	LD	A,(PLAY)
	INC	A
	LD	(PLAY),A
ONE	MOV	(PLAY+15),(BONUS)
	LD	(PLAY+16),A
	MOV	(PLAY1),(SCITY)
	LD	B,A
	LD	HL,PLAY1+2
ONE1	LD	(HL),1
	INC	HL
	DJNZ	ONE1
	LD	A,R
	AND	A
	JR	NZ,RSET1
	LD	A,5AH
RSET1	LD	(RANTAB),A
	MOV	(PLAYER),2
GLOOP	LD	A,(PLAY1)
	LD	B,A
	LD	A,(PLAY2)
	OR	B
	JP	Z,HIGHSC
	LD	A,(PLAYER)
	XOR	3
	LD	(PLAYER),A
	DEC	A
	JR	Z,P1
	LD	HL,PLAY2
	LD	A,(HL)
	OR	A
	JR	NZ,SWAP
	LD	HL,PLAY1
	LD	A,1
P2	LD	(PLAYER),A
	JR	SWAP
P1	LD	HL,PLAY1
	LD	A,(HL)
	OR	A
	JR	NZ,SWAP
	LD	HL,PLAY2
	LD	A,2
	JR	P2
SWAP	LD	DE,PCB
	LD	BC,18
	LDIR
	LD	A,-1
	OUT	TUARTA+TIMER5,A
	MOV	(EXPLCT),0
	LD	(MISSCT),A
	LD	(SMISCT),A
; PAGE 15 FOLLOWS
	LD	HL,EXPLTB+2
	LD	DE,3
	LD	B,NUMEXP
	LD	A,-1
ECLR	LD	(HL),A
	ADD	HL,DE
	DJNZ	ECLR
	LD	A,(SET)
	SRL	A
CCALC	LD	B,A
	SUB	10
	JP	P,CCALC
	MOV	(COLOR),B
	LD	A,(SET)
	INC	A
	INC	A
	CP	12
	JR	NC,NCOR2
	AND	14
	RRCA
	JR	NCOR3
NCOR2	LD	A,6
NCOR3	LD	(TIMES),A
	ADD	A,-6
	JR	NC,PGEN9
	MOV	(TIMES),6
PGEN9	LD	HL,POINTS
	LD	DE,POINTS+1
	LD	BC,34
        LD      (HL),0
	LDIR
	LD	DE,PBASE
	LD	HL,POINTS
	LD	B,5
PGEN	PUSH	BC
	MOV	C,(TIMES)
PGEN3	PUSH	HL
	PUSH	DE
	LD	B,7
	OR	A
PGEN1	LD	A,(DE)
	ADC	A,(HL)
	CP	10
	CCF
	JR	NZ,PGEN2
	SUB	10
	SCF
PGEN2	LD	(HL),A
	INC	DE
	INC	HL
	DJNZ	PGEN1
	POP	DE
	POP	HL
	DEC	C
	JR	NZ,PGEN3
	LD	BC,7
	ADD	HL,BC
	EX	DE,HL
	ADD	HL,BC
	EX	DE,HL
	POP	BC
	DJNZ	PGEN
	LD	A,(COLOR)
	LD	HL,COLUSE
READY2	DEC	A
	JP	M,READY3
; PAGE 16 FOLLOWS
	INC	HL
	INC	HL
	INC	HL
	INC	HL
	JR	READY2
READY3	MOV	(READY7),4
	LD	DE,FM1
READY4	LD	BC,CUSETB
	LD	A,(HL)
	LD	(DE),A
	INC	DE
	INC	HL
READY5	DEC	A
	JP	M,READY6
	INC	BC
	JR	READY5
READY6	MOV	(DE),(BC)
	INC	DE
	GET	READY7
	DEC	A
	LD	(READY7),A
	JR	NZ,READY4
	MOV	(CREADY),(COLOR)
	LD	HL,CBTAB
DREADY	GET	CREADY
	DEC	A
	LD	(CREADY),A
	JP	M,EREADY
	INC	HL
	JR	DREADY
EREADY	MOV	(FREADY),(HL)
	LD	HL,COLTAB
GREADY	GET	FREADY
	DEC	A
	LD	(FREADY),A
	JR	Z,HREADY
	INC	HL
	INC	HL
	JR	GREADY
HREADY	MOV	(COL1),(HL)
	INC	HL
	MOV	(COL2),(HL)
	MOV	(READY8),-1
	MOV	(READY9),A
	MOV	B,(BG)
	CMP	B,C01
	JR	Z,READYA
	CMP	B,C11
	JR	Z,READYA
	MOV	(READY8),0
READYA	CMP	B,C10
	JR	Z,READYB
	CMP	B,C11
	JR	Z,READYB
	XOR	A
	LD	(READY9),A
READYB	LD	HL,SC1
	LD	A,SC2/256
READYC	LD	(HL),0
READY8	EQU	$-1
	INC	HL
	CP	H
	JR	NZ,READYC
	ADD	A,8
READYD	LD	(HL),0
READY9	EQU	$-1
; PAGE 17 FOLLOWS
	INC	HL
	CP	H
	JR	NZ,READYD
	MOV	(ONOFF),(EM)
	CALL	FORE
	MOV	(ONOFF),(FM)
	CALL	MISPO
	LD	HL,CITTAB
	MOV	(READYE),6
	LD	DE,CITPOS
READYF	GET	READYE
	DEC	A
	LD	(READYE),A
	JP	M,READYG
	LD	A,(DE)
	INC	DE
	ADD	A,5
	LD	B,A
	MOV	C,(DE)
	INC	DE
	LD	A,(HL)
	INC	HL
	AND	A
	JR	Z,READYF
	PUSH	HL
	PUSH	DE
	PUSH	BC
	MOV	(ONOFF),(EX)
	PUT	39
	PUT	40
	POP	BC
	MOV	(ONOFF),(FM)
	PUT	41
	PUT	42
	POP	DE
	POP	HL
	JR	READYF
READYG	MOV	(ONOFF),(EM)
	LD	BC,40*256+67
	PUT	(TIMES)
	MOV	(ONOFF),(FM)
	LD	HL,BREADY
	CALL	MESOUT
	LD	BC,2560+55
	LD	HL,AREADY
	CALL	MESOUT
	MOV	(ONOFF),(EM)
	PUT	(PLAYER)
	CALL	SCOR
	MOV	(ONOFF),(FM)
	CALL	SCORW
	LD	C,6
ONE5	LD	D,7FH
ONE4	LD	B,15
ONE3	LD	A,7FH
	SUB	D
ONE2	DEC	A
	JR	NZ,ONE2
	LD	A,(SOUND)
	CPL
	LD	(SOUND),A
	OUT	19H,A
	DJNZ	ONE3
	DEC	D
	CMP	D,40H
	JR	NZ,ONE4
; PAGE 18 FOLLOWS
	DEC	C
	JR	NZ,ONE5
	MOV	(ONOFF),(BG)
	LD	C,32
RCL	LD	B,0
RCL1	DOT
	INC	B
	JP	P,RCL1
	INC	C
	CMP	C,96
	JR	NZ,RCL
	LD	HL,BASES
	LD	B,3
TGYYRU	LD	(HL),10
	INC	HL
	DJNZ	TGYYRU
	LD	HL,MISSTB+12
	LD	DE,13
	LD	B,19
MCLEAR	LD	(HL),255
	ADD	HL,DE
	DJNZ	MCLEAR
	MOV	(WAVEC),0
	LD	(SWAVEC),A
	LD	(WWAVE),A
	LD	A,(SET)
	CP	10
	JR	C,NCOR
	AND	1
	ADD	A,10
NCOR	LD	L,A
	LD	H,0
	ADD	HL,HL
	ADD	HL,HL
	LD	DE,SETDT
	ADD	HL,DE
	LD	IX,NMIS
	LD	B,4
NCOR1	MOV	(IX),(HL)
	INC	IX
	INC	HL
        DJNZ    NCOR1
	LD	A,(SET)
	CP	10
	JR	C,NOAD
	CALL	RAND
	AND	3
	NEG
	LD	B,A
	LD	A,(NMIS)
	ADD	A,B
	LD	(NMIS),A
	CALL	RAND
	AND	1
	NEG
	LD	B,A
	LD	A,(NSHP)
	ADD	A,B
	LD	(NSHP),A
	CALL	RAND
	AND	2
	NEG
	LD	B,A
	LD	A,(NSMS)
	ADD	A,B
	LD	(NSMS),A
; PAGE 19 FOLLOWS
NOAD	MOV	(OFFCRS),0
	LD	(NFIN),A
	DEC	A
	LD	(TRCT),A
	LD	(TRCT+3),A
	LD	(TRCT+6),A
	LD	(TARCIT),A
	LD	(TARCIT+2),A
	LD	(TARCIT+4),A
	LD	A,(NSHP)
	OR	A
	CALL	NZ,SLRBM1
	MOV	(KILCIT),3
SETLOP	LD	HL,MISSCT
	LD	B,7
	LD	A,2
	EX	AF,AF'
	XOR	A
SETLP1	EX	AF,AF'
	CP	B
	JR	Z,NSETLP
	EX	AF,AF'
	OR	(HL)
	EX	AF,AF'
NSETLP	EX	AF,AF'
	INC	HL
	DJNZ	SETLP1
	AND	A
	JR	NZ,STLP99
	LD	A,(SPACE+3)
	INC	A
	JR	NZ,STLP99
	GET	NFIN
	INC	A
	LD	(NFIN),A
	CP	30
	JP	Z,EOS
	JR	STLP98
STLP99	MOV	(NFIN),A
STLP98	LD	A,(KILCIT)
	LD	B,3
	LD	HL,BASES
STLP96	OR	(HL)
	INC	HL
	DJNZ	STLP96
	JP	NZ,STLP97
	LD	HL,NMIS
	LD	B,3
	XOR	A
STLP95	LD	(HL),A
	INC	HL
	DJNZ	STLP95
STLP97	GET	CRSUP
	INC	A
	LD	(CRSUP),A
	AND	1
	JP	Z,STLP7
	IN	A,18H
	AND	40H
	LD	B,A
	LD	A,(CCT)
	CP	B
	JR	Z,STLP1
	MOV	(CCT),B
	AND	A
	JR	Z,STLP2
; PAGE 20 FOLLOWS
	MOV     (ONOFF),(EM)
	LD	A,(LSTN)
	JR	STLP3
CPLACE	LD	D,A
	SHIFT	R,4
	AND	15
	CP	10
	JR	C,CPLAC1
	INC	A
CPLAC1	PUSH	DE
	CALL	PNUM
	POP	DE
	LD	A,15
	AND	D
	CP	10
	JR	C,CPLAC2
	INC	A
CPLAC2	JP	PNUM
STLP2	MOV	(ONOFF),(FM)
	MOV	(LSTN),(SET)
STLP3	LD	BC,30*256+127
	CALL	CPLACE
STLP1	IN	A,18H
	AND	20H
	LD	B,A
	LD	A,(CST)
	CP	B
	JR	Z,STLP4
	MOV	(CST),B
	AND	A
	JR	Z,STLP5
	MOV	(ONOFF),(EM)
	LD	A,(LCTN)
	JR	STLP6
STLP5	MOV	B,(CITYS)
	MOV	(ONOFF),(FM)
	LD	A,(CITYB)
	ADD	A,B
	LD	(LCTN),A
STLP6	LD	BC,85*256+127
	CALL	CPLACE
STLP4	LD	HL,BASES
	LD	B,3
	LD	A,(MISSCT)
STLP8	OR	(HL)
	INC	HL
	DJNZ	STLP8
	JR	NZ,STLP8A
	GET	OFFCRS
	INC	A
	JP	Z,STLP7
	MOV	(OFFCRS),-1
	MOV	(ONOFF),(BG)
	LD	BC,(SITEY)
	CALL	SITED
	MOV	(MSPD),4
	JP	STLP7
STLP8A	LD	HL,SITEY
	LD	(SITEOY),HL
	IN	A,(1AH)
	CPL
	AND	A,128
	AND	0FEH
	RRCA
	CP	5
	JR	NC,SITE1
; PAGE 21 FOLLOWS
        LD      A,5
SITE1	CP	122
	JR	C,SITE2
	LD	A,122
SITE2	LD	(SITEX),A
	IN	A,19H
	CPL
	ADD	A,128
	AND	0FEH
	RRCA
	CP	13
	JR	NC,SITE3
	LD	A,13
SITE3	CP	101
	JR	C,SITE4
	LD	A,101
SITE4	LD	(SITEY),A
	LD	A,(SITEY)
	CP	C
	JR	NZ,SITEOK
	LD	A,(SITEX)
	CP	B
	JP	Z,STLP7
SITEOK	MOV	(ONOFF),(BG)
	LD	BC,(SITEOY)
	CALL	SITED
STLP9   MOV     (ONOFF),(FM)
        LD      BC,(SITEY)
        CALL    SITED
STLP7	CALL	LAUNCH
	MOV	(SMARTU),0
	LD	IX,MISSTB
	MOV	(LINCOL),(FM)
	LD	A,(EX)
	CALL	MOVE
	MOV	(MISSCT),(COUNT)
	GET	FHJ
	INC	A
	LD	D,A
	LD	A,(MSPD)
	CP	D
	LD	A,D
	JR	NC,QXA
	MOV	(LINCOL),(EM)
	LD	IX,EMISTB
	LD	A,CTEST
	CALL	MOVE
	MOV	(EMISCT),(COUNT)
	CALL	SMOVE
	CALL	SMLR
	CALL	KCITY
	CALL	MCHECK
	XOR	A
QXA	LD	(FHJ),A
	GET	QQQ
	INC	A
	CP	24
	JR	NZ,QQ1
	CALL	EXPLUD
	XOR	A
QQ1	LD	(QQQ),A
	GET	QQ4
	INC	A
	CP	12
	JR	NZ,QQ5
	CALL	SPACER
; PAGE 22 FOLLOWS
	XOR	A
QQ5	LD	(QQ4),A
	JP	SETLOP
TAKEAR	XOR	A
	JR	ARDRW3
PUTAR	LD	A,1
ARDRW3	LD	(ARPUT2),A
	LD	HL,SC1+85*16
	LD	IX,Q3DAT1
	LD	IY,Q3DAT
	CALL	ARDRW
	LD	HL,SC1+117*16
ARDRW	LD	B,3
ARDRW1	LD	A,(IY)
	INC	IY
	ADD	A,L
	LD	L,A
	LD	DE,-16
	CALL	ARPUT
	LD	DE,16
	DEC	HL
	CALL	ARPUT
	INC	HL
	DJNZ	ARDRW1
	RET
ARPUT	LD	C,3
ARPUT1	MOV	(HL),(IX)
	GET	ARPUT2
	AND	A
	JR	NZ,ARPUT3
	LD	(HL),0
ARPUT3	INC	IX
	DEC	C
	RET	Z
	ADD	HL,DE
	JR	ARPUT1
DFCTY	DB	14,15,16,15,24,14,10,13,19,30,19,15,29,255	;	DEFNED	CITIES
Q3DAT	DB	5,3,4,3,4,4
Q3DAT1	AR1
	AR2
	AR3
	AR1
	AR1
	AR3
END	LD	HL,SC1
	MOV	(COL1),(COLTAB)
	MOV	(COL2),(COLTAB+1)
	CALL	DAZMOD
	LD	A,[SC1+800H]/256
END1	LD	(HL),-1
	INC	HL
	CP	H
	JR	NZ,END1
	LD	A,[SC2+800H]/256
END2	LD	(HL),0
	INC	HL
	CP	H
	JR	NZ,END2
	MOV	(INT2B),1
	OUT	TUARTA+TIMER2,A
	MOV	(ONOFF),C11
	LD	BC,64+64*256
	DOT
	LD	D,1
END14	CALL	END3
	INC	D
	CMP	D,64
; PAGE 23 FOLLOWS
	JR	NZ,END14
	MOV	(ONOFF),C00
	MOV	(END16),6
	LD	HL,ENDXY
	LD	(END19),HL
	LD	HL,ENDCH
	LD	(END20),HL
END17	GET	END16
	DEC	A
	LD	(END16),A
	JP	M,END18
	LD	BC,(0)
END19	EQU	$-2
	LD	HL,(END19)
	INC	HL
	INC	HL
	LD	(END19),HL
	LD	HL,0
END20	EQU	$-2
	LD	A,(HL)
	INC	HL
	LD	(END20),HL
	LD	HL,ENDST
	LD	DE,9
END21	DEC	A
	JP	M,END22
	ADD	HL,DE
	JR	END21
ENDCH	DB	0,1,2,2,3,4
ENDST	DB	16,16,16,16,16,16,16,16,127
	DB	65,65,65,65,127,65,65,65,65
	DB	127,64,64,64,112,64,64,64,127
	DB	65,65,65,67,69,73,81,97,65
	DB	124,34,33,33,33,33,33,34,124
ENDXY	DB	61,50,61,74,61,98
	DB	94,51,94,75,94,99
AREADY	DB	17,15,30,10,28,15,11,14,35,10,26,22,11,35,15
	DB	28,10,255 ; GET READY PLAUER
BREADY	DB	34,10,12,25,24,31,29,255
END22	LD	D,9
END23	LD	E,7
	MOV	(END25),(HL)
	INC	HL
	PUSH	BC
END24	GET	END25
	RRCA
	LD	(END25),A
	CALL	C,END26
	DEC	B
	DEC	B
	DEC	B
	DEC	E
	JR	NZ,END24
	POP	BC
	DEC	C
	DEC	C
	DEC	C
	DEC	D
	JR	NZ,END23
	LD	HL,20000
	CALL	DELAY
	JP	END17
END26	PUSH	HL
	PUSH	DE
	PUT	44
	POP	DE
; PAGE 24 FOLLOWS
	POP	HL
	LD	A,-6
	ADD	A,B
	LD	B,A
	RET
END18	MOV	(ONOFF),C01
	LD	D,63
END15	CALL	END3
	DEC	D
	JR	NZ,END15
	JP	GBEGIN
END3	LD	BC,64+64*256
	LD	A,D
	ADD	B
	LD	B,A
	LD	E,D
END5	DEC	E
	JP	M,END6
	DEC	C
	DEC	B
	DOT
	JR	END5
END6	LD	E,D
END7	DEC	E
	JP	M,END8
	INC	C
	DEC	B
	DOT
	JR	END7
END8	LD	E,D
END9	DEC	E
	JP	M,END10
	INC	C
	INC	B
	DOT
	JR	END9
END10	LD	E,D
END11	DEC	E
	JP 	M,END12
	DEC	C
	DEC	B
	DOT
	JR	END11
END12	LD	HL,1000
	CALL	DELAY
	RET
INMES	DB	26,28,15,29,29,10,12,31,30,30,25,24,10,30,18,28,15,15,10,16
; PRESS BUTTON THREE F
; CONSULT PAGE 23A 
	DB	25,28,10,25,24,15,10,26,22,11,35,15,28,18,29,30,11,28
; OR ONE PLAYER STAR
	DB	30,10,25,28,10,26,28,15,29,29,10,12,31,30,30,25,24,10,16,25,31,28,10,16,25
; T OR PRESS BUTTON FOUR FO
	DB	28,10,30,33,25,10,26,22,11,35,15,28,10,29,30,11,28,30
; R TWO PLAYER START
	DB	43,10,10,30,18,19,29,10,32,15,28,29,19,25,24,10,25
; . THIS VERSION O
	DB	16,10,23,19,29,29,19,22,15,10,13,25,23,23,11,24,11
; F MISSILE COMMAND
	DB	10,33,11,29,10,33,28,19,30,30,15,24
; WAS WRITTEN
	DB	10,12,35,10,20,15,16,16,21,15,29,24,15,28
; BY JEFF KESNER
	DB	10,11,24,14,10,23,11,30,30,18,15,33,10,16,28
; AND MATTHEW FR
	DB	11,24,13,15,35,43
; ANCEY.
; PAGE 25 FOLLOWS
	DB	10,10,20,31,24,15,10,1,6,10,10,1,9,8,2
; JUNE 16, 1982
	DB	10,10,10,10,10,10,10,10,10,10,10,10,10,10,10,10,10,10,10,10,10
BMESPL	EQU	$-INMES
BCITY	DB	12,25,24,31,29,10,13,19,30,35,10,16,25,28,255	; BONUS CITY FOR
BCITY8	DB	10,8,0,0,0,25	;	8000
MESOUT	LD	A,(HL)
	CP	255
	INC	HL
	RET	Z
	PUSH	HL
	CALL	PNUM
	POP	HL
	JR	MESOUT
CHARO	MOV	(CR),6
	ADD	HL,DE
PL1	LD	E,6
	LD	D,B
	LD	A,(HL)
PL	RRCA
	PUSH	AF
	CALL	C,XY
	DEC	B
	POP	AF
	DEC	E
	JR	NZ,PL
	LD	B,D
	DEC	C
	GET	CR
	DEC	A
	LD	(CR),A
	INC	HL
	JR	NZ,PL1
	RET
RAND	PUSH	BC
	PUSH	IX
	LD	IX,RANTAB
	LD	B,8
	LD	A,(IX)
RAND1	RLCA
	RLCA
	RLCA
	XOR	(IX)
	RLA
	RLA	
	RL	(IX-3)
	RL	(IX-2)
	RL	(IX-1)
	LD	A,(IX)
	RLA
	LD	(IX),A
	DJNZ	RAND1
	POP	IX
	POP	BC
	RET
	DB	4AH,78,113
RANTAB	DB	25H
PNUMOB	LD	E,A
	LD	D,0
	LD	H,D
	LD	L,E
	ADD	HL,DE
	ADD	HL,DE
	LD	D,H
	LD	E,L
	LD	HL,ZERO
; PAGE 26 FOLLOWS
	ADD	HL,DE
	PUSH	BC
	CALL	CHARO
	POP	BC
	LD	A,6
	ADD	A,B
	LD	B,A
	RET
ZERO	DB	14,25,21,19,14,0	; 0
	DB	14,4,4,12,4,0           ; 1
	DB	31,8,6,17,14,0          ; 2
	DB	14,1,6,1,14,0           ; 3
	DB	2,2,31,18,18,0          ; 4
	DB	30,1,30,16,31,0         ; 5
	DB	14,17,30,16,15,0        ; 6
	DB	8,4,2,1,31,0            ; 7
	DB	14,17,14,17,14,0        ; 8
	DB	14,1,31,17,14,0		; 9
	DB	0,0,0,0,0,0		; SPACE
	DB	17,31,17,10,4,0		; A
	DB	30,17,30,17,30,0        ; B
	DB	14,17,16,17,14,0	; C
	DB	30,17,17,17,30,0        ; D
        DB      31,16,30,16,31,0        ; E
	DB	16,16,31,16,31,0        ; F
	DB	14,18,23,16,14,0        ; G
	DB	17,17,31,17,17,0        ; H
	DB	14,4,4,4,14,0		; I
	DB	14,17,1,1,1,0           ; J
	DB	9,10,12,10,9,0		; K
	DB	31,16,16,16,16,0        ; L
	DB	17,17,21,31,27,0        ; M
	DB	17,19,21,25,17,0	; N
	DB	14,17,17,17,14,0	; O
	DB	16,16,30,17,30,0        ; P
	DB	15,19,21,17,14,0        ; Q
	DB	17,18,30,17,30,0	; R
	DB	30,1,14,16,15,0         ; S
	DB	4,4,4,4,31,0            ; T
	DB	14,17,17,17,17,0	; U
	DB	4,10,17,17,17,0         ; V
	DB	17,27,21,17,17,0        ; W
	DB	17,10,4,10,17,0		; X
	DB	4,4,4,10,17,0		; Y
	DB	31,8,4,2,31,0		; Z
	DB	4,0,6,17,14,0           ; ?
        DB      0,31,0,31,0,0           ; =
        DB      32,49,31,21,4,4	        ; 39 & 40 CITY WHITE, 41,42 CITY FM Color	
	DB	1,63,27,26,16,16,31,14,0,0,0,0,62,8,0,0,0,0	; 
	DB	24,24,0,0,0,0,7,7,7,0,0,0
        DB      21,14,31,14,21,0	; '*'
	DB	0,4,0,4,0,0             ; ':'
NUMZER  EQU     ($-ZERO) / 6        

MBONP	DB	12,25,24,31,29,10,26,25,19,24,30,29,255
CITBN	DB	12,25,24,31,29,10,13,19,30,35,255
COLTAB	DB	113,126,122,125,124,123
CBTAB	DB	1,2,3,3,1,1,2,3,3,1
COLUSE	DB	2,1,0,3,2,1,0,3,2,1,0,3
	DB	1,2,0,3,1,0,2,3,2,0,3,1
	DB	1,0,2,3,1,0,2,3,1,0,3,2,0,2,1,3
PBASE	DB	5,2,0,0,0,0,0,0,0,0,1,0,0,0
	DB	5,2,1,0,0,0,0,5,0,0,0,0,0,0
	DB	0,0,1,0,0,0,0
PLAY	DB	1
CITPOS	DB	41,121,71,119,27,116,86,118,14,117,101,119
MISPOS	DB	7,109,62,109,120,109
LOWTAB	DB	4,59,115
MISDIS	DB	0,1,-2,3,4,0,-6,3,4,0,4,0,-10,3,4,0,4,0,4,0
CIRC2	DB	0,0,1,0,0,-1,-1,0,-1,0,0,1,0,1,1,0,1,0,80H
CIRC3	DB	2,0,0,-1,-1,-1,-1,0,-1,0,-1,1,0,1,0,1,1,1
	DB	1,0,1,0,1,-1,80H
CIRC4	DB	3,0,0,-1,-1,-1,-1,-1,-1,0,-1,0,-1,1,-1,1,0,1
	DB	0,1,1,1,1,1,1,0,1,0,1,-1,1,-1,80H
CIRC5	DB	4,0,0,-1,0,-1,-1,0,0,-1,-1,0,0,-1,-1,0,-1,0
	DB	-1,0,-1,0,0,1,-1,0,0,1,-1,0,0,1,0,1,0,1,0,1
	DB	1,0,0,1,1,0,0,1,1,0,1,0,1,0,1,0,0,-1,1,0,0,-1
	DB	1,-1,0,-1,80H
CIRC6	DB	5,0,0,-1,0,-1,-1,-1,0,-1,-1,0,-1,-1,-1,0,-1,0
	DB	-1,-0,-1,0,-1,1,-1,0,0,1,-1,1,0,1,0,1,0,1,0,1
	DB	1,1,0,1,1,0,1,1,1,0,1,0,1,0,1,0,1,-1,1,0,0,-1
	DB	1,1,0,1,1,0,1,1,1,0,1,0,1,0,1,0,1,-1,1,0,0,-1
	DB	1,-1,0,-1,80H
CIRC7	DB	6,0,0,-1,0,-1,-1,-1,0,-1,-1,-1,-1,0,-1,-1,-1,0
	DB	-1,0,-1,0,-1,0,-1,1,-1,0,-1,1,0,1,-1,1,0,1,0,1
	DB	0,1,0,1,1,1,0,1,1,1,1,0,1,1,1,0,1,0,1,0,1,0
	DB	1,-1,1,0,1,-1,0,-1,1,-1,0,-1,80H
CIRC8	DB	7,0,0,-1,0,-1,-1,-1,0,-1,-1,-1,-1,-1,-1,0
	DB	-1,-1,-1,0,-1,0,-1,0,-1,0,-1,1,-1,0,-1,1,-1,1
	DB	0,1,-1,1,0,1,0,1,0,1,0,1,1,1,0,1,1,1,1,1,1,0
	DB	1,1,1,0,1,0,1,0,1,0,1,-1,1,0,1,-1,1,-1,0,-1
	DB	1,-1,0,-1,80H
CIRC9	DB	8,0,0,-1,0,-1,-1,-1,0,-1,-1,-1,-1,-1,-1,-1
	DB	-1,0,-1,-1,-1,0,-1,0,-1,0,-1,0,-1,1,-1,0,-1,1
	DB	-1,1,-1,1,0,1,-1,1,0,1,0,1,0,1,0,1,0,1,1,0,0,1
	DB	1,1,1,1,1,1,1,0,1,1,1,0,1,0,1,0,1,0,1,-1,1,0
; PAGE 27 FOLLOWS
	DB	1,-1,1,-1,1,-1,0,-1,1,-1,0,-1,128
CIRCTB	DW	CIRC2,CIRC3,CIRC4,CIRC5,CIRC6,CIRC7
	DW	CIRC8,CIRC9
; Set data/details
; +0 = Number of missiles
; +1 = Number of bombers
; +2 = Number of smart missiles
; +3 = Speed of missiles
SETDT	DB	12,0,0,28
	DB	16,2,0,20
	DB	20,3,0,13
	DB	16,4,0,9
	DB	20,4,0,8
	DB	16,3,1,7
	DB	20,4,3,7
	DB	12,3,2,6
	DB	16,4,4,6
	DB	20,5,5,6
	DB	25,5,6,5
	DB	23,4,6,5
SCITY	DB	4
CUSETB	DB	C00,C01,C10,C11
BONUS	DB	10H
SOUND	DB	7FH
INT1	DI
        PUSH	AF
        GET     PACEDZ
        DEC     A
        LD      (PACEDZ),A
        JR      NZ,INT1C
        CALL    DAZMOD
        MOV     (PACEDZ),30
	JR	INT1C	; Use simulated page flipping in DAZMOD, Dec 2020
        
	DI
	PUSH	AF
INT1D	IN	A,0EH
	AND	40H
	JP	NZ,INT1D
	GET	INT1A
	INC	A
	LD	(INT1A),A
	AND	1
	JP	Z,INT1B
	GET	COL1
	OUT	0FH,A
	LD	A,SC1/512+80H
DZOFF1	EQU	$-1
	OUT	0EH,A
	JP	INT1C
INT1B	GET	COL2
	OUT	0FH,A
	LD	A,SC2/512+80H
DZOFF2	EQU	$-1
	OUT	0EH,A
INT1C	LD	A,248
	OUT	TUARTA+TIMER1,A
	POP	AF
	EI
	RET
; Draw the land
FORE	LD	IY,CRATF
	LD	BC,127  ; x,y 0,127
	LD	HL,FTAB
	LD	DE,3*256
FORE1	CALL	FORES           ; Get 4 bits from FTAB
	AND	A
	JR	Z,FORE3         ; 0 means next is repeat count and then height
	CALL	REPT            ; Draw a column to given height
FORE8	XOR	A
	ADD	B
	JP	P,FORE1         ; B goes 0-127
	RET
FORE3	CALL	FORES           ; Get 4 bits from FTAB
	LD	(FORE5),A
	CALL	FORES
FORE6	CALL	REPT            ; Loop here for 4 bit quantity from FTAB times
	GET	FORE5           ; REPT draws a column
	DEC	A
	LD	(FORE5),A
	LD	A,E
	JR	NZ,FORE6
; PAGE 28 FOLLOWS
	JR	FORE8
; Enters with height-5
REPT	PUSH	DE
	ADD	A,5
	MOV	E,A
FORE4	DOT
	DEC	C
	DEC	E               ; The height of this column of terrain
	JR	NZ,FORE4
	INC	C
	LD	(IY),C          ; Save height of this column for cratering data (CRATF)
	INC	IY
	INC	B               ; Next X
	LD	C,127           ; y = 127
	POP	DE
	RET
FORES	LD	A,E
	RLD
	DEC	D
	JR	NZ,FORE2        ; 3 rotates to fix memory around, so done with this FTAB entry
	INC	HL
	RLD
	LD	D,2
FORE2	LD	E,A
	RET
; Foreground drawing data (and missile bases) in nybbles, either height or 0 for repeat followed by count and height
FTAB	DB	0EDH,07,0CDH,0E9H,60H,0D5H,0DH,65H,40H,0B1H,23H,69H,0CEH,0D0H,7CH,0DEH ; 1982 Listing didn't have 0B1H
	DB	0C9H,60H,0C3H,45H,0DH,45H,54H,0CH,34H,69H,0CEH,0D0H,7CH,0DEH
INT2	DI
	PUSH	AF
	LD	A,(100H)
INT2A	EQU	$-2
	OUT	19H,A
	LD	A,(INT2A)
	INC	A
	LD	(INT2A),A
	JR	Z,INT2C
	LD	A,2
INT2B	EQU	$-1
	OUT	TUARTA+TIMER2,A
INT2D	POP	AF
	EI
	RET
INT2C	LD	A,(INT2B)
	INC	A
	LD	(INT2B),A
	CP	22
	JR	Z,INT2D
	JP	INT2B-1
NSTAT	EQU	7
NDEL	EQU	12
NUMEXP	EQU	20
EXPLUD	LD	A,(EXPLCT)
	CP	NDEL
; THE FOLLOWING JUMP IS TO GET AROUND PAUSE
	JR	EXUD1
	NEG
	ADD	A,NDEL
	LD	D,A
	MOV	(ONOFF),CTEST
EXUD2	LD	E,20
EXUD3	DOT
	DEC	E
	JR	NZ,EXUD3
	DEC	D
	JR	NZ,EXUD2
EXUD1	LD	D,NUMEXP
	MOV	(EXPLCT),0
; PAGE 29 FOLLOWS
	LD	HL,EXPLTB
XPLUD8	LD	B,(HL)
	INC	HL
	LD	C,(HL)
	INC	HL
	LD	A,(HL)
	INC	A
	JR	Z,XPLD4A
	LD	A,(EXPLCT)
	INC	A
	LD	(EXPLCT),A
	MOV	(ONOFF),(EX)
	MOV	(CRATP),0
	LD	A,(HL)
	CP	NSTAT
	JR	C,XPLUD1
	MOV	(ONOFF),(BG)
	MOV	(CRATP),1
	LD	A,2*NSTAT-1
	SUB	(HL)
XPLUD1	ADD	A,A
	PUSH	DE
	PUSH	HL
	LD	HL,CIRCTB
	LD	E,A
	LD	D,0
	ADD	HL,DE
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
XPLUD2	LD	A,(DE)
	INC	DE
	CP	128
	JR	Z,XPLUD3
	ADD	A,B
	LD	B,A
	LD	A,(DE)
	INC	DE
	ADD	A,C
	LD	C,A
	CP	123
	JR	NC,XPLUD2
	CP	6
	JR	C,XPLUD2
	GET	CRATP
	AND	A
	PUSH	HL
	JR	Z,NCRAT
	MOV	(ONOFF),(BG)
	LD	H,CRATF/256
	LD	A,7FH
	AND	B
	LD	L,A
	LD	A,(HL)
	CP	C
	JR	NC,NCRAT
	MOV	(ONOFF),(EM)
	JR	NCRAT1
NCRAT	EQU	$
NCRAT1	POP	HL
	DOT
	JR	XPLUD2
XPLUD3	POP	HL
	INC	(HL)
	LD	A,-2*NSTAT
	ADD	A,(HL)
; PAGE 30 FOLLOWS
	JR	NZ,XPLUD4
	LD	(HL),-1
	LD	A,(EXPLCT)
	DEC	A
	LD	(EXPLCT),A
XPLUD4	POP	DE
XPLD4A	DEC	D
	RET	Z
	INC	HL
	JP	XPLUD8
SITED	DEC	B
	DEC	B
	CALL	XYEX
	INC	B
	CALL	XYEX
	INC	B
	CALL	XYEX
	INC	B
	CALL	XYEX
	INC	B
	CALL	XYEX
	DEC	C
	DEC	C
	DEC	B
	DEC	B
	CALL	XYEX
	INC	C
	CALL	XYEX
	INC	C
	INC	C
	CALL	XYEX
	INC	C
XYEX	MOV	E,(ONOFF)
	MOV	(ONOFF),CTEST
	DOT
	LD	D,A
	MOV	(ONOFF),E
	LD	A,(EX1)
	CP	D
	RET	Z
	EXX
	RES	3,H
	JP	XYACMP
TARGET	LD	(IX+11),A
	LD	(IX+12),H
	LD	(IX),B
	LD	(IX+1),C
	LD	(IX+7),B
	LD	(IX+8),C
	LD	A,D
	SUB	B
	LD	(IX+5),1
	JP	P,D1
	LD	(IX+5),-1
	NEG
D1	LD	(IX+2),A
	LD	A,E
	SUB	C
	LD	(IX+6),1
	JP	P,D2
	LD	(IX+6),-1
	NEG
D2	LD	(IX+3),A
; PAGE 31 FOLLOWS
	SRL	A
	LD	(IX+4),A
	LD	(IX+9),D
	LD	(IX+10),E
	RET
MOVE	LD	D,8
	LD	(TIPCOL),A
	MOV	(COUNT),0
MOVENM	EQU	$
INCR3	LD	A,(IX+12)
	INC	A
	JR	Z,INCR4
	BIT	7,(IX+11)
	JR	Z,UPD
	LD	A,(IX+11)
	XOR	1
	LD	(IX+11),A
	AND	1
	JR	NZ,INCR1
UPD	LD	E,1
	PUSH	DE
	GET	LINCOL
	LD	(ONOFF),A
	CALL	LINE
	JR	Z,EXP
	POP	DE
INCR1	LD	A,(COUNT)
	INC	A
	LD	(COUNT),A
INCR4	DEC	D
	RET	Z
	LD	BC,13
	ADD	IX,BC
	JR	INCR3
EXP	LD	A,(IX+12)
	CP	20H
	JR	NC,EXPER
	SRL	A
	CP	6
	JR	NC,EXPZZ
	LD	HL,CITTAB
	LD	C,A
	LD	B,0
	ADD	HL,BC
	LD	A,(HL)
	OR	A
	JR	Z,EXPER
	LD	A,(CITYS)
	DEC	A
	LD	(CITYS),A
	LD	A,(KILCIT)
	DEC	A
	LD	(KILCIT),A
EXPQW	LD	(HL),0
	MOV	(EXPLV),EXPLLL
	JR	EXPER
EXPZZ	SUB	5
	LD	(BL),A
	CALL	OUTM
	LD	A,(BL)
	DEC	A
	LD	HL,BASES
	LD	C,A
	LD	B,0
	ADD	HL,BC
	LD	A,(HL)
; PAGE 32 FOLLOWS
	OR	A
	JR	NZ,EXPQW
EXPER	CALL	EXPSET
	POP	DE
	JR	INCR4
LINE	LD	B,(IX)
	LD	C,(IX+1)
	DOT
	GET	TIPCOL
	LD	(ONOFF),A
LINE1	LD	A,(IX+9)
	CP      (IX)
	JR	NZ,NOENDL
	LD	A,(IX+1)
	CP	(IX+10)
	RET	Z
NOENDL	LD	A,(IX+3)
	CP	(IX+4)
	JR	C,NOX
	LD	A,(IX+1)
	ADD	A,(IX+6)
	LD	(IX+1),A
	LD	A,(IX+4)
	ADD	A,(IX+2)
	LD	(IX+4),A
	CP	(IX+3)
	JR	C,YTF
NOX	LD	A,(IX)
	ADD	A,(IX+5)
	LD	(IX),A
	LD	A,(IX+4)
	SUB	(IX+3)
	LD	(IX+4),A
YTF	LD	B,(IX)
	LD	C,(IX+1)
	DOT
	DEC	E
	JR	NZ,LINE1
	OR	1
	RET
SEARCH	LD	B,8
SERCHR	LD	DE,13
SERCH1	LD	A,(IX+12)
	INC	A
	RET	Z
	ADD	IX,DE
	DJNZ	SERCH1
	RET
QCHECK	MOV	(HFLAG),0
	MOV	(ONOFF),CTEST
	MOV	E,(EX1)
QCHK1	LD	A,(HL)
	CP	80H
	RET	Z
	ADD	A,B
	LD	B,A
	INC	HL
	LD	A,(HL)
	INC	HL
	ADD	A,C
	LD	C,A
	DOT
	CP	E
	JR	NZ,QCHK1
	GET	HFLAG
	INC	A
	LD	(HFLAG),A
; PAGE 33 FOLLOWS
	JR	QCHK1
SQUARE	DB	1,1,0,-1,0,-1,-1,0,-1,0,0,1,0,1,1,0,128
EXPFND	LD	IY,EXPLTB
	LD	DE,3
	LD	B,NUMEXP
EXPF1	LD	A,(IY+2)
	INC	A
	RET	Z
	ADD	IY,DE
	DJNZ	EXPF1
	RET
EXPSET	CALL	EXPFND
	JR	NZ,EXP2
	MOV	(IY),(IX)
	MOV	(IY+1),(IX+1)
	MOV	(IY+2),0
EXP2	CALL	KCITR
	LD	(IX+12),-1
	GET	SMARTU
	OR	A
	RET	NZ
	MOV	(IX+9),(IX)
	MOV	(IX+10),(IX+1)
	MOV	(IX),(IX+7)
	MOV	(IX+1),(IX+8)
	LD	A,(IX+3)
	SRL	A
	LD	(IX+4),A
	LD	E,0
	MOV	(TIPCOL),(BG)
	LD	(ONOFF),A
	JP	LINE
MCHECK	LD	IX,EMISTB
	LD	D,0
MCHK1	LD	A,(IX+12)
	INC	A
	PUSH	DE
	JP	Z,MCHKA2
	LD	B,(IX)
	LD	C,(IX+1)
	LD	A,C
	CP	110
	JR	NC,MCHKA
	CP	7
	JR	C,MCHKA
	MOV	(ONOFF),CTEST
	DOT
	LD	E,A
	LD	A,(EX1)
	CP	E
	JR	NZ,MCHKA1
	LD	E,NUMEXP
	LD	IY,EXPLTB
SKPE5	LD	A,(IY+2)
	INC	A
	JR	Z,SKPE1
	DEC	A
	CP	NSTAT+1
	JR	C,SKPE2
	NEG
	ADD	A,2*NSTAT+1
SKPE2	INC	A
	LD	D,A
	LD	A,(IY)
	SUB	B
; PAGE 34 FOLLOWS
	JP	P,SKPE3
	NEG
SKPE3	CP	D
	JR	NC,SKPE1
	LD	A,(IY+1)
	SUB	C
	JP	P,SKPE4
	NEG
SKPE4	CP	D
	JR	C,SKPE6
SKPE1	INC	IY
	INC	IY
	INC	IY
	DEC	E
	JR	NZ,SKPE5
	JR	MCHKA1
SKPE6	PUSH	DE
	CALL	EXPSET
	XOR	A
	CALL	SINC
	MOV	(EXPLV),EXPLLL
	POP	DE
MCHKA1	MOV	(ONOFF),(EX)
	EXX
	RES	3,H
	CALL	XYACMP
	JR	MCHKA2
MCHKA	MOV	(ONOFF),(EX)
	DOT
MCHKA2	LD	BC,13
	POP	DE
	ADD	IX,BC
	DEC	D
	JP	NZ,MCHK1
	RET
KCITY	GET	WWAVE
	OR	A
	JR	Z,KCIT9
	DEC	A
	LD	(WWAVE),A
	RET
KCIT9	GET	WAVEC
	INC	A
	AND	15
	LD	(WAVEC),A
	JP	Z,KCIT8
	GET	SWAVEC
	CP	4
	RET	NC
KCIT7	LD	A,(NMIS)
	OR	A
	RET	Z
KCITYA	CALL	RAND
	AND	7
	JR	Z,MIRV
	LD	A,(SPACE+3)
	INC	A
	JP	Z,MIRV
	CP	1
	JR	Z,SP1
	LD	H,0
	LD	L,4
	JR	SP2
SP1	LD	H,7
	LD	L,0
SP2	LD	A,(SPACE+2)
; PAGE 35 FOLLOWS
	DEC	A
	JR	Z,SP3
	LD	A,H
	NEG
	LD	H,A
SP3	LD	A,(SPACE)
	CP	15
	JR	C,MIRV
        CP      112
        JR      NC,MIRV
	ADD	A,H
	LD	B,A
	LD	A,(SPACE+1)
	ADD	A,L
	LD	C,A
	JR	KCIT1
MIRV	CALL	RAND
	AND	3
	JR	NZ,TOPS
	LD	IX,EMISTB
	LD	D,8
	LD	BC,13
MIRV2	LD	A,(IX+12)
	INC	A
	JR	Z,MIRV1
	CALL	RAND
	AND	3
	JR	NZ,MIRV1
	LD	A,(IX+1)
	CP	10
	JR	C,MIRV1
	CP	80
	JR	NC,MIRV1
	LD	C,A
	LD	B,(IX)
KCIT1	CALL	RAND
	AND	3
	INC	A
	LD	H,A
	LD	A,4
	LD	(SWAVEC),A
	JR	KCIT2
MIRV1	ADD	IX,BC
	DEC	D
	JR	NZ,MIRV2
TOPS	LD	H,1
	CALL	RAND
	AND	7FH
	LD	B,A
	LD	C,5
	LD	A,(SWAVEC)
	INC	A
	LD	(SWAVEC),A
KCIT2	LD	A,(NMIS)
	OR	A
	RET	Z
	EXX
SXCZ	CALL	TCITY
	LD	L,A
	PUSH	DE
	LD	IX,EMISTB
	CALL	SEARCH
	LD	A,L
	EXX
	POP	DE
	JR	NZ,KCIT5
; PAGE 36 FOLLOWS
	PUSH	HL
	LD	H,A
	CALL	RAND
	AND	80H
KCIT10	CALL	TARGET
	POP	HL
	LD	A,(NMIS)
	DEC	A
	LD	(NMIS),A
	DEC	H
	JR	NZ,KCIT2
	RET
KCIT5	LD	A,95
	LD	(WWAVE),A
	MOV	(WAVEC),0
	LD	(SWAVEC),A
	RET
KCIT8	MOV	(SWAVEC),0
	JP	KCIT7
SMLR	LD	A,(NSMS)
	OR	A
	RET	Z
	LD	A,(WAVEC)
	OR	A
	RET	NZ
	LD	A,(NMIS)
	CP	4
	JR	C,SMLR1
	CALL	RAND
	AND	63
	RET	NZ
SMLR1	LD	A,1
	LD	(WAVEC),A
	LD	A,(WWAVE)
	CP	6
	JR	NC,SMLR2
	LD	A,6
	LD	(WWAVE),A
SMLR2	LD	B,3
	LD	IX,SMISTB
	CALL	SERCHR
	RET	NZ
	CALL	TCITY
	LD	H,A
	CALL	RAND
	AND	7FH
	LD	B,A
	LD	C,6
	CALL	RAND
	AND	80H
	CALL	TARGET
	LD	A,(NSMS)
	DEC	A
	LD	(NSMS),A
	RET
SMARTP	DB	1,0,-1,1,-1,-1,1,-1,128
LAUNCH	IN	A,(18H)
	CPL
	AND	7
	LD	(BUTTON),A
	LD	HL,MISPOS
	LD	DE,BASES
	LD	BC,FBASE
	MOV	(BL),A
LANCH	GET	BUTTON
	SRL	A
; PAGE 37 FOLLOWS
	LD	(BUTTON),A
	JP	NC,LANCH1
	LD	A,(BC)
	OR	A
	JP	NZ,LANCH2
	PUSH	HL
	PUSH	DE
	PUSH	BC
	LD	IX,MISSTB
	CALL	SEARCH
	JP	NZ,BOINK
	POP	BC
	POP	DE
	PUSH	DE
	PUSH	BC
	LD	A,(DE)
	OR	A
	JP	Z,BOINK
	LD	B,(HL)
	INC	HL
	LD	C,(HL)
	PUSH	DE
	PUSH	BC
	LD	D,A
	LD	HL,MISDIS
MISO	LD	A,(HL)
	ADD	A,B
	LD	B,A
	INC	HL
	LD	A,(HL)
	ADD	A,C
	LD	C,A
	INC	HL
	DEC	D
	JR	NZ,MISO
	MOV	(ONOFF),(EM)
	CALL	LISSIM
	POP	BC
	POP	DE
	LD	A,(DE)
	DEC	A
	LD	(DE),A
	CALL	Z,OUTM
	CP	3
	CALL	Z,LOWM
	MOV	(WLEV),8
	MOV	(WLEN),80
	LD	A,8
	OUT	TUARTA+TIMER3,A
	PUSH	BC
	MOV	B,(SITEX)
	MOV	C,(SITEY)
	LD	E,C
	LD	D,B
	MOV	(ONOFF),(EM)
	DOT
	INC	B
	INC	C
	DOT
	DEC	C
	DEC	C
	DOT
	DEC	B
	DEC	B
	DOT
	INC	C
; PAGE 38 FOLLOWS
	INC	C
	DOT
	POP	BC
	GET	BL
	CP	2
	LD	A,80H
	JR	NZ,BL1
	XOR	A
BL1	LD	A,80H
	CALL	TARGET
LANCH3	POP	BC
	POP	DE
	POP	HL
	LD	A,1
	JR	LANCH2
LANCH1	XOR	A
LANCH2	LD	(BC),A
	INC	BC
	INC	DE
	INC	HL
	INC	HL
	LD	A,(BL)
	INC	A
	CP	4
	RET	Z
	LD	(BL),A
	JP	LANCH
BOINK	MOV	(OMV),8
	MOV     (OML),30
	LD	A,8
	OUT	TUARTA+TIMER4,A
	JR	LANCH3
; Draw a single missile in it's base
LISSIM	DOT
	INC	C
	DOT
	INC	B
	INC	C
	DOT
	DEC	B
	DEC	B
	DOT
	INC	B
	DEC	C
	DEC	C
	RET
OUTM	PUSH	BC
	MOV	(ONOFF),(EM)
	CALL	MESPOS
	LD	HL,LOWMIS
	PUSH	BC
	CALL	MESOUT
	POP	BC
	LD	HL,OUTMIS
	MOV	(ONOFF),(BG)
	CALL	MESOUT
	POP	BC
	XOR	A
	RET
MESPOS	EXX
	MOV	E,(BL)
	LD	D,0
	LD	HL,LOWTAB
	DEC	E
	ADD	HL,DE
	LD	B,(HL)
	LD	C,127
; PAGE 39 FOLLOWS
	PUSH	BC
	EXX
	POP	BC
	RET
LOWM	PUSH	BC
	MOV	(ONOFF),(BG)
	CALL	MESPOS
	LD	HL,LOWMIS
	CALL	MESOUT
	MOV	(MLN),5
	MOV	(WSL),1
	MOV	(ML1),255
	LD	A,8
	OUT	TUARTA+TIMER5,A
	POP	BC
	RET
LOWMIS	DB	22,25,33,255
OUTMIS	DB	25,31,30,255
EOS	LD	A,(SET)
	INC	A
	LD	(SET),A
	MOV	(SPACE+3),-1
	MOV	(ONOFF),(BG)
	MOV	B,(SITEX)
	MOV	C,(SITEY)
	CALL	SITED
	LD	BC,28*256+45
	LD	HL,MBONP
	MOV	(ONOFF),(FM)
	CALL	MESOUT
	LD	HL,46*256+49
	LD	(BMXYP),HL
	CALL	TSCL
	LD	DE,MISPOS
	MOV	(MMMMM),3
	LD	HL,BASES
MMMLP	MOV	B,(DE)
	INC	DE
	MOV	C,(DE)
	INC	DE
	XOR	A
	ADD	A,(HL)
	JR	Z,MMM1
	PUSH	DE
	LD	DE,MISDIS
MMM2	LD	A,(DE)
	ADD	A,B
	LD	B,A
	INC	DE
	LD	A,(DE)
	ADD	A,C
	LD	C,A
	INC	DE
	PUSH	DE
	PUSH	HL
	PUSH	BC
	MOV	(ONOFF),(EM)
	CALL	LISSIM
	MOV	(ONOFF),(FM)
	LD	BC,0	
BMXYP	EQU	$-2
	CALL	LISSIM
	LD	HL,(BMXYP)
	INC	HL
	INC	HL
	INC	HL
; PAGE 40 FOLLOWS
	INC	H
	LD	(BMXYP),HL
	LD	A,3
	CALL	SINC
	MOV	(ALTSP1),56
	LD	A,3
	CALL	BONSC
	CALL	SNCK
	POP	BC
	POP	HL
	POP	DE
	DEC	(HL)
	JR	NZ,MMM2
	POP	DE
MMM1	GET	MMMMM
	DEC	A
	LD	(MMMMM),A
	JR	Z,CSBM
	INC	HL
	LD	A,(BMXYP)
	ADD	A,4
	LD	(BMXYP),A
	MOV	(BMXYP+1),46
	JP	MMMLP
CSBM	LD	HL,50*256+70
	LD	(BCXYP),HL	; BONUS CITY CALC
	CALL	TSCL
	LD	DE,CITPOS
	LD	HL,CITTAB
	MOV	(CCCCC),7
CCCLP	MOV	B,(DE)
	INC	DE
	MOV	C,(DE)
	INC	DE
	GET	CCCCC
	DEC	A
	LD	(CCCCC),A
	JR	Z,EOS1
	LD	A,(HL)
	AND	A
	INC	HL
	JR	Z,CCCLP
	PUSH	DE
	PUSH	HL
	LD	A,5
	ADD	A,B
	LD	B,A
	PUSH	BC
	MOV	(ONOFF),(BG)
	PUT	39
	PUT	40
        POP     BC
	PUT	41
	PUT	42
	LD	BC,0
BCXYP	EQU	$-2
	PUSH	BC
	MOV	(ONOFF),(EX)
	PUT	39
	PUT	40
	POP	BC
	MOV	(ONOFF),(EM)
	PUT	41
	PUT	42
	INC	B
	INC	B
; PAGE 41 FOLLOWS
	LD	(BCXYP),BC
	LD	A,4
	CALL    SINC ; ADD 40 POINTS
	MOV	(ALTSP1),70
	LD	A,4
	CALL	BONSC
	CALL	SNCK	;	CLICK SOUND
	POP	HL
	POP	DE
	JP	CCCLP
EOS1	LD	B,4
EOS3	CALL	DELAY1
	DJNZ	EOS3
	LD	A,(CITYB)
	AND	A
	JR	Z,EOS12
	LD	HL,CITTAB
	LD	B,6
	MOV	(BFLAG),0
EOS13	LD	A,(HL)
	AND	A
	JR	NZ,EOS14
	LD	(HL),1
	LD	A,(CITYS)
	INC	A
	LD	(CITYS),A
	LD	A,(CITYB)
	DEC	A
	LD	(CITYB),A
	MOV	(BFLAG),1
	JR	Z,EOS15
EOS14	INC	HL
	DJNZ	EOS13
	GET	BFLAG
	AND	A
	JR	Z,EOS12
EOS15	LD	HL,BCITYM
	LD	BC,34*256+90
	MOV	(ONOFF),(FM)
	CALL	MESOUT	
	CALL	NOYZ
EOS12	LD	DE,PLAY1
	LD	A,(PLAYER)
	CP	1
	JR	Z,EOS4
	LD	DE,PLAY2
EOS4	LD	HL,PCB
	LD	BC,18
	LDIR
	LD	A,(CITYS)
	AND	A
	JP	NZ,EOS10
	LD	BC,42*256+20
	MOV	(ONOFF),(FM)
	LD	HL,GOM
	CALL	MESOUT
	LD	BC,46*256+26
	LD	HL,GOM2
	MOV	(GOM1),(PLAYER)
	CALL	MESOUT
	CALL	DELAY1
EOS10	JP	GLOOP
GOM	DB	17,11,23,15,10,25,32,15,28,255	;	GAME OVER
GOM2	DB	26,22,11,35,15,28,10,0,255	;	PLAYER 0
GOM1	EQU	$-2
BCITYM	DB	12,25,24,31,29,10,13,19,30,35,255
; PAGE 42 FOLLOWS
SNCK	PUSH	BC
	LD	HL,100H
	LD	B,7
SNCK1	LD	A,(HL)
	OUT	19H,A
	INC	L
	JR	NZ,SNCK1
	DJNZ	SNCK1
	LD	HL,400H
	CALL	DELAY
	POP	BC
	RET
BONSC	PUSH	AF
	MOV	(ALTSP),1
	LD	(BBCHK),A
	LD	DE,TSC
	LD	HL,SCORE
	LD	BC,7
	LDIR
	LD	DE,SCORE
	LD	HL,BCORE
	LD	BC,7
	LDIR
	POP	AF
	CALL	SINC
	MOV	(ALTSP),0
	LD	(BBCHK),A
	LD	DE,BCORE
	LD	HL,SCORE
	LD	BC,7
	LDIR
	LD	DE,SCORE
	LD	HL,TSC
	LD	BC,7
	LDIR
	RET
NOYZ	LD	B,10
NOYZ1	CALL	RAND
	AND	15
	INC	A
	LD	E,A
	LD	A,5
NOYZ2	ADD	A,7
	DEC	E
	JR	NZ,NOYZ2
	LD	E,A
	LD	HL,2000H
NOYZ3	LD	C,E
NOYZ4	DEC	HL
	LD	A,H
	OR	L
	JR	Z,NOYZ5
        DEC     C
        JR      NZ,NOYZ4
	LD	A,(SOUND)
	CPL
	LD	(SOUND),A
	OUT	19H,A
	OUT	1BH,A
	JR	NOYZ3
NOYZ5	DJNZ	NOYZ1
	RET
TSCL	LD	HL,BCORE
	LD	B,7
TSCL1	LD	(HL),0
	INC	HL
; PAGE 43 FOLLOWS
	DJNZ	TSCL1
	RET
WHSI	DI
	PUSH	AF
	GET	WLEN
	DEC	A
	LD	(WLEN),A
	JR	NZ,WHSI1
	MOV	(WLEN),255
	GET	WLEV
	DEC	A
	LD	(WLEV),A
	JR	NZ,WHSI1
	POP	AF
	EI
	RET
WHSI1	LD	A,(100H)
WPOS	EQU	$-2
	OUT	1BH,A
	LD	A,(WPOS)
	INC	A
	LD	(WPOS),A
	LD	A,(WLEV)
	OUT	TUARTA+TIMER3,A
	POP	AF
	EI
	RET
OMI	DI
	PUSH	AF
	LD	A,(OMV)
	AND	A
	JR	Z,EXPLI
	GET	OML
	DEC	A
	LD	(OML),A
	JR	NZ,OMI1
	MOV	(OML),30
	GET	OMV
	DEC	A
	LD	(OMV),A
	CP	1
	JR	NZ,OMI1
NINTO	LD	A,255
	OUT	TUARTA+TIMER4,A
	POP	AF
	EI
	RET
OMI1	LD	A,7FH
OMS	EQU	$-1
	CPL
	OUT	1BH,A
	LD	(OMS),A
	LD	A,(OMV)
	OUT	TUARTA+TIMER4,A
	POP	AF
	EI
	RET
EXPLI	CMP	EXPLLM,(EXPLV)
	JR	Z,NINTO
	GET	EXPLL
	INC	A
	CP	12
	JR	NZ,EXPLI1
	LD	A,EXPLLM
EXPLV	EQU	$-1
EXPLLL	EQU	30
; PAGE 44 FOLLOWS
EXPLLM	EQU	64
	INC	A
	LD	(EXPLV),A
	CP	EXPLLM
	JR	Z,NINTO
	XOR	A
EXPLI1	LD	(EXPLL),A
	LD	A,(100H)
EXPLP	EQU	$-2
	OUT	1BH,A
	LD	A,(EXPLP)
	INC	A
	LD	(EXPLP),A
	LD	A,(EXPLV)
	OUT	TUARTA+TIMER4,A
	POP	AF
	EI
	RET
MLOW	MOV	(MLN),5
	MOV	(WSL),1
	MOV	(ML1),255
	LD	A,8
	OUT	TUARTA+TIMER5,A
	RET
MLOWI	DI
	PUSH	AF
	LD	A,(MLN)
	AND	A
	JP	Z,SHWH
	LD	A,30H
ML1	EQU	$-1
	DEC	A
	LD	(ML1),A
	JR	NZ,ML2
	GET	MLN
	DEC	A
	LD	(MLN),A
	JR	NZ,ML3
	JP	NINT
ML3	GET	WSL
	XOR	1
	LD	(WSL),A
	MOV	(ML1),255
ML2	LD	A,(WSL)
	AND	A
	JR	Z,ML4
	LD	A,7FH
MLS	EQU	$-1
	CPL
	LD	(MLS),A
	OUT	19H,A
ML4	LD	A,8
	OUT	TUARTA+TIMER5,A
	POP	AF
	EI
	RET
SHWH	LD	A,(SMISCT)
	AND	A
	JR	Z,SHWHP
	LD	A,3
SMARL	EQU	$-1
	DEC	A
	JR	NZ,MARL1
	LD	A,12
MARL1	LD	(SMARL),A
	JR	NZ,MARL2
; PAGE 45 FOLLOWS
	LD	A,4
SMAR1	EQU	$-1
	DEC	A
	CP	3
	JR	NZ,SMAR2
	LD	A,11
SMAR2	LD	(SMAR1),A
MARL2	LD	A,(SOUND)
	CPL
	LD	(SOUND),A
	OUT	19H,A
	LD	A,(SMAR1)
	OUT	TUARTA+TIMER5,A
	POP	AF
	EI
	RET
SHWHP	LD	A,(SPACE+3)
	CP	-1
	JP	Z,NINT
	LD	A,(SOUND)
	CPL
	LD	(SOUND),A
	OUT	19H,A
	LD	A,5
SHWHL	EQU	$-1
SHWHFT	EQU	150
SHWHFS	EQU	140
	DEC	A
	LD	(SHWHL),A
	JR	NZ,SHWH1
	MOV	(SHWHL),A
	LD	A,SHWHFT
SHWHF	EQU	$-1
	DEC	A
	CP	SHWHFS
	JR	NZ,SHWH2
	LD	A,SHWHFT
SHWH2	LD	(SHWHF),A
SHWH1	LD	A,(SHWHF)
	OUT	TUARTA+TIMER5,A
	POP	AF
	EI
	RET
NINT	LD	A,-1
	OUT	TUARTA+TIMER5,A
	POP	AF
	EI
	RET
KEYI	DI
	PUSH	AF
	LD	A,01H
	CP	1BH
	JR	NZ,KEYIX
	LD	SP,0FFH
	MOV	(0),0C3H
	LD	HL,0
KEYIP	EQU	$-2
	LD	(1),HL
	DI
	JP	0
KEYIX	POP	AF
	EI
	RET
KEYO	DI
	PUSH	AF
	POP	AF
; PAGE 46 FOLLOWS
	EI
	RET
TCITY	CALL	RAND
	AND	3
	JP	Z,TCITY1
	LD	A,(KILCIT)
	OR	A
	JR	Z,TCITYZ
	LD	HL,TARCIT
	LD	B,3
	LD	D,0
	LD	A,-1
TCITYA	CP	(HL)
	JR	Z,TCITYB
	INC	D
TCITYB	INC	HL
	INC	HL
	DJNZ	TCITYA
	LD	A,(KILCIT)
	CP	D
	JR	NC,TCITY9
TCITYZ	LD	A,(CITYS)
	CP	6
	JP	Z,TCITY1
TCITYC	LD	D,0
	JR	TCITY2
TCITY9	LD	A,(CITYS)
	OR	A
	JR	Z,TCITYC
	LD	D,1
TCITY2	CALL	RAND
	AND	7
	CP	6
	JR	NC,TCITY2
	PUSH	DE
	LD	E,A
	ADD	A,A
	LD	C,A
	LD	D,0
	LD	HL,CITTAB
	ADD	HL,DE
	POP	DE
	LD	A,(HL)
	CP	D
	JR	NZ,TCITY2
	LD	A,D
	OR	A
	JR	Z,TCITY4
	CALL	SCH
	JR	Z,TCITY3
	PUSH	BC
	LD	C,-1
	CALL	SCH
	POP	BC
	JR	NZ,TCITY2
	LD	(HL),C
	INC	HL
	LD	(HL),1
	JR	TCITY4
TCITY3	INC	HL
	INC	(HL)
TCITY4	LD	HL,CITPOS
	LD	E,C
	LD	D,0
	ADD	HL,DE
	LD	D,(HL)
; PAGE 47 FOLLOWS
	INC	HL
	LD	E,(HL)
	LD	A,C
        CP      12
        JR      NC,TCITY5
        LD      A,D
        ADD     A,6
        LD      D,A
        LD      A,C
	RET
TCITY5	LD	A,E
	ADD	A,7
	LD	E,A
	LD	A,C
	RET
TCITY1	CALL	RAND
	AND	3
	JR	Z,TCITY1
	DEC	A
	ADD	A,A
	ADD	A,12
	LD	C,A
	JR	TCITY4
SCH	LD	HL,TARCIT
	LD	B,3
	LD	A,C
SCH1	CP	(HL)
	RET	Z
	INC	HL
	INC	HL
	DJNZ	SCH1
	RET
TARCIT	DS	6
KILCIT	DS	1
KCITR	LD	C,(IX+12)
	CALL	SCH
	RET	NZ
	INC	HL
	DEC	(HL)
	RET	NZ
	DEC	HL
	LD	(HL),-1
	RET
; Satellite draw instructions 81h jumps to next color 80h is end of data
SDATR	DB	0,0,-1,-1,3,0,-1,1,81H
	DB	1,0,1,-1,-3,0,-1,-2,0,5,-1,-1,0,-1,0,-1,0,-1,-1,-2,0,7
	DB	7,0,0,-7,81H
	DB	2,-1,-1,1,-2,1,1,1,0,1,0,1,0,1,-1,1,2,1,1,1,-9,0
	DB	1,-1,0,-7,-1,-1,81H
	DB	-1,0,0,9,9,0,0,-9,80H
; Bomber draw instructions, skips FM, then draws EM and then BG
BDATR	DB	81H,5,-2,-2,-2,-2,1,-2,1,-1,1,-2,1,3,1,1,1,2,1,81H
	DB	2,0,-1,-1,-1,-1,3,-1,1,-1,0,-1,-4,0,1,-1,1,-1,80H
SPACER	LD	A,(SPACE+3)
	INC	A
	JP	Z,SLRBMF
	MOV	B,(SPACE)
	LD	A,(SPACE+2)
	ADD	A,B
	CP	135
	JR	Z,SPEND
	CP	248
	JR	NZ,OKSPQ
SPEND	MOV	(SPACE+3),-1
	RET
OKSPQ	LD	(SPACE),A
	LD	B,A
	MOV	C,(SPACE+1)
; PAGE 48 FOLLOWS
	LD	A,(SPACE+3)
	LD	HL,BDATR
	OR	A
	JR	Z,OKSPQ1
	LD	HL,SDATR
OKSPQ1	MOV	(KFLAG),0
	LD	(COLL1),A
	MOV	(COLL),(FM)
	MOV	E,(EX1)
	LD	IX,FM
KDRW	LD	A,(HL)
	INC	HL
	CP	80H
	JR	Z,OKSPQ2
	CP	81H
	JR	Z,NXTCOL        ; NEXT COLOR
	LD	D,A
	LD	A,(SPACE+2)
	DEC	A
	LD	A,D
	JR	NZ,NONEG
	NEG
NONEG	ADD	A,B
	LD	B,A
	LD	A,C
	ADD	A,(HL)
	LD	C,A
	INC	HL
	LD	A,B
	CP	80H
	JR	NC,KDRW
	MOV	(ONOFF),CTEST
	DOT
	CP	E
	JR	NZ,OKDRW
	GET	KFLAG
	INC	A
	LD	(KFLAG),A
OKDRW	GET	COLL
	LD	(ONOFF),A
	EXX
	RES	3,H
	CALL	XYACMP
	JR	KDRW
NXTCOL	LD	A,(COLL1)
	INC	A
	INC	A               ; CYCLE THROUGH FM,EM,BG
	LD	(COLL1),A
	LD	A,(IX+0)
COLL1	EQU	$-1
	LD	(COLL),A
	JR	KDRW
OKSPQ2	LD	A,(SPACE+3)
	ADD	A,A
	ADD	A,A
	ADD	A,4
	LD	B,A
	LD	A,(KFLAG)
	CP	B
	RET	C
	CALL	EXPFND
	JR	NZ,KILQA
	MOV	(IY),(SPACE)
	MOV	(IY+1),(SPACE+1)
	MOV	(IY+2),0
KILQA	MOV	(SPACE+3),-1
; PAGE 49 FOLLOWS
	MOV	(EXPLV),EXPLLL
	LD	A,1
	JP	SINC
SLRBMF	LD	A,(NSHP)
	OR	A
	RET	Z
	LD	A,(NMIS)
	OR	A
	RET	Z
	CP	8
	JR	C,SLRBM1
	LD	A,(WAVEC)
	OR	A
	RET	NZ
	CALL	RAND
	AND	63
	RET	NZ
SLRBM1	CALL	RAND    ; POSITION BOMBER
	AND	1
	LD	(SPACE+3),A
	CALL	RAND
	AND	63
	ADD	A,15
	LD	(SPACE+1),A
	CALL	RAND
	AND	1
	JR	Z,FGBH
	MOV	(SPACE),135
	LD	A,-1
	JR	FGBH1
FGBH	MOV	(SPACE),248
	LD	A,1
FGBH1	LD	(SPACE+2),A
	LD	A,(NSHP)
	DEC	A
	LD	(NSHP),A
	RET
SINC	PUSH	AF      ; INCREASE SCORE
	MOV	(ONOFF),(BG)
	CALL	SCORW
	POP	AF
	LD	C,A
	ADD	A,A
	ADD	A,A
	ADD	A,A
	SUB	C
	LD	E,A
	LD	D,0
	LD	HL,POINTS
	ADD	HL,DE
	EX	DE,HL
	LD	HL,SCORE+6
	OR	A
	LD	B,7
SINC2	LD	A,(DE)
	ADC	A,(HL)
	CP	10
	CCF
	JR	NC,SINC1
	SUB	10
	SCF
SINC1	LD	(HL),A
	DEC	HL
	INC	DE
	DJNZ	SINC2
	GET	BBCHK
; PAGE 50 FOLLOWS
	OR	A
	JR	NZ,SINC9
	LD	A,(SCORE+2)
	SHIFT	R,4
	LD	B,A
	LD	A,(SCORE+3)
	OR	B
	LD	B,A
	LD	A,(BCHECK)
	CP	B
	JR	NZ,SINC9
	LD	B,A
	LD	A,(CITYB)
	INC	A
	LD	(CITYB),A
	LD	A,(BONIS)
	ADD	A,B
	DAA
	LD	(BCHECK),A
SINC9	MOV	(ONOFF),(FM)
SCORW	LD	A,(PLAYER)
	DEC	A
	LD	BC,4+4*256
	JR	Z,SCORW1
	LD	BC,4+87*256
SCORW1	GET	ALTSP
	AND	A
	JR	Z,SCRW1A
	LD	B,5
	LD	C,0
ALTSP1	EQU	$-1
SCRW1A	LD	HL,SCORE-1
	JP	SCOR4
SSSSSS	DB	1,0,0,-1,-1,0,-1,0,0,1,0,1,0,1,1,0,1,0
	DB	0,-1,-1,0,128,128
SMOVE	LD	IX,SMISTB
	MOV	(SMARTS),3
	LD	(SMARTU),A
	MOV	(LINCOL),(BG)
	MOV	(TIPCOL),(EX)
	MOV	(COUNTS),0
SMOVE2	LD	A,(IX+12)
	INC	A
	JP	Z,SMOVE3
	LD	A,105
	CP	(IX+1)
	JR	C,SMOVE4
	LD	B,(IX)
	LD	C,(IX+1)
	LD	HL,SSSSSS
	CALL	QCHECK
	LD	A,(HFLAG)
	CP	4
	JR	C,SMOVE4
	CALL	EXPSET
	LD	A,2
	CALL	SINC	
	JP	SMOVE3
SMOVE4	MOV	(ONOFF),(BG)
	CALL	SMART
	LD	D,1
	CALL	MOVENM
	MOV	(ONOFF),CTEST
	LD	HL,0
	LD	B,(IX)
	LD	C,(IX+1)
; PAGE 51 FOLLOWS
	LD	A,C
	CP	105
	JP	NC,DODGE3
	MOV	D,(EX1)
	DEC	B
	DEC	B
	DEC	B
        DEC     B
	INC	C
	INC	C
	INC	C
	INC	C
	DOT
	CP	D
	JR	NZ,DODGE1
	LD	HL,101H
DODGE1	LD	A,B
	ADD	A,8
	LD	B,A
	DOT
	CP	D
	JR	NZ,DODGE4
	LD	HL,0FF01H
DODGE4	LD	A,B
	ADD	A,-4
	LD	B,A
	DOT
	CP	D
	JR	NZ,DODGE5
	LD	L,-1
DODGE5	LD	A,H
	OR	L
	LD	B,(IX)
	LD	C,(IX+1)
	JR	Z,DODGE3
	MOV	(ONOFF),(BG)
	DOT
	LD	A,H
	ADD	A,B
	LD	B,A
	LD	A,L
	ADD	A,C
	LD	C,A
	LD	D,(IX+9)
	LD	E,(IX+10)
	LD	H,(IX+12)
	LD	A,(IX+11)
	CALL	TARGET
	MOV	(ONOFF),(EX)
	DOT
DODGE3	MOV	(ONOFF),(EM)
	CALL	SMART
	GET	COUNTS
	INC	A
	LD	(COUNTS),A
SMOVE3	GET	SMARTS
	DEC	A
	LD	(SMARTS),A
	JR	Z,SMOVE5
	LD	DE,13
	ADD	IX,DE
	JP	SMOVE2
SMOVE5	MOV	(SMISCT),(COUNTS)
        RET
 ; DELAY 4 MICROSECONDS PER HL COUNT
DELAY1	LD	HL,0
DELAY	DEC	HL
; PAGE 52 FOLLOWS
	LD	A,H
	OR	L
	JR	NZ,DELAY
	RET
SMART	LD	B,(IX)
	LD	C,(IX+1)
	LD	HL,SMARTP
KDRAW	LD	A,(HL)
	CP	128
	INC	HL
	RET	Z
	ADD	A,B
	LD	B,A
	LD	A,(HL)
	ADD	A,C
	LD	C,A
	INC	HL
	LD	A,(ONOFF)
	EX	AF,AF'
	MOV	(ONOFF),CTEST
	DOT
	LD	E,A
	LD	A,(EX1)
	CP	E
	EX	AF,AF'
	LD	(ONOFF),A
	EX	AF,AF'
	JR	Z,KDRAW
	EXX
	RES	3,H
	CALL	XYACMP
	JR	KDRAW
DISKST	LD	DE,HIGHS1
	LD	C,26
	CALL	5
	LD	HL,MISFNM
	CDOS	134
	RET
DISKW	CALL	DISKST
	CDOS	22
	CDOS	15
	LD	DE,HIGHS1
	LD	HL,HIGHS
	LD	BC,128
	LDIR
	CDOS	21
	JP	EXDISK
MISFNM	DM	'MISSILEH.IGH'
	DB	1FH
DISKR	CALL	DISKST
	CDOS	15
	CP	-1
	JR	NZ,DISKR1
	CALL	HIGHST
	JP	DISKW
DISKR1	CDOS	20
	LD	DE,HIGHS
	LD	HL,HIGHS1
	LD	BC,128
	LDIR
EXDISK	CDOS	16
	RET
FBASE	DB	0,0,0,0

; Make ZEROB table (bit blit table)
; Hand compiled from the C version
; C version does gymnastics so it can print pretty comments       
YBIT    DS      1
        
MKZERO  LD      IY,ZEROB
        LOOPB   ZCHAR,NUMZER
        LOOPB   ZYOFF,2
        LOOPB   ZXOFF,4
MKZRX1  CALL    CLSZ            ; Uses IX
        LD      B,5
MKZERX  LD      C,5
        MOV     (YBIT),1 SHL 5
MKZRC1  LD      A,(ZCHAR)       ; int color = zero[c][x] & (1 << y);
        ADD     A,A             ; max 108
        LD      E,A
        LD      D,0
        LD      L,A
        LD      H,0
        ADD     HL,HL
        ADD     HL,DE           ; C * 6
        LD      E,B
        ADD     HL,DE
        LD      DE,ZERO
        ADD     HL,DE
        LD      A,(YBIT)
        AND     A,(HL)
        JR      Z,MKNOBT
        PUSH    BC
        LD      A,5             ; xy(5-y + xOff, 5-x + yOff, color); 
        SUB     C               ; 5 - y
        LD      HL,ZXOFF        
        ADD     A,(HL)          ; + xOff
        LD      E,A
        LD      A,5
        SUB     B               ; 5 - x
        LD      HL,ZYOFF        
        ADD     A,(HL)          ; + yOff
        LD      C,A             ; xy Y parm = 5-x+yOff
        LD      B,E             ; xy X parm = 5-y + xOff
        CALL    XY16            
        POP     BC
MKNOBT  DEC     C               ; 2 closing braces in C
        LD      A,(YBIT)
        RRCA
        LD      (YBIT),A
        JP      P,MKZRC1
        DEC     B
        JP      P,MKZERX
        
; Dump the bytes from the screen after the XY plot of the character is done        
        PUSH    BC
        LD      C,0             ; for (int y = 0; y < 4; y++)
MKDUY   LD      B,0             ;   for (int x = 0; x < 3; x++)
MKDUX   LD      A,C             ;       screen[x + (y << 4)]
        SHIFT   L,4
        ADD     A,B
        LD      L,A
        LD      H,SC1/256
        LD      A,(HL)
        LD      (IY),A
        INC     IY
        INC     B
        LD      A,3
        CP      B
        JR      NZ,MKDUX        ; x<3
        INC     C
        LD      A,4
        CP      C
        JR      NZ,MKDUY        ; 2 closing braces in C
        
        POP     BC              ; Retrieve x, y
        ENDLPB  ZXOFF
        ENDLPB  ZYOFF
        ENDLPB  ZCHAR
        RET
; Just draw the character on top of whatever is already there.        
PNUM    PUSH    BC
        LD      L,A
        LD      E,A
        LD      A,-5
        ADD     A,B
        LD      B,A
        LD      A,-5
        ADD     A,C             ; Convert top-left to bottom right coords
        LD      C,A
        LD      H,0
        LD      D,0
        ADD     HL,HL
        ADD     HL,DE           ; * 3
        ADD     HL,HL
        ADD     HL,HL           ; * 12
        ADD     HL,HL
        ADD     HL,HL
        ADD     HL,HL           ; A * 12 * 8
        LD      IX,ZEROB
        EX      DE,HL
        ADD     IX,DE           ; Offset of bit blit table for this character
        LD      D,12*4
        BIT     0,C             ; Y
        JR      NZ,Y1
        LD      D,0
Y1      LD      A,B
        AND     3               ; x portion
        LD      E,A
        ADD     A,A
        ADD     A,E             ; 3 * x portion
        ADD     A,A             ; * 6 (MAX 18)
        ADD     A,A             ; * 12 (MAX 36)
        ADD     A,D             ; MAX 84
        LD      E,A
        LD      D,0
        ADD     IX,DE           ; Offset into the table for this character
        LD      A,C             ; IX has ZEROB table position
; Compute memory position on screen from x & y in BC        
        AND     3EH
        LD      L,A
        LD      H,0
        ADD     HL,HL
        ADD     HL,HL
        ADD     HL,HL           ; y portion * 16 bytes per line
        LD      A,B
        AND     3CH
        SHIFT   R,2
        LD      E,A
        LD      D,0
        ADD     HL,DE           ; + x portion
        LD      DE,SC1
        ADD     HL,DE           ; Offset of byte on screen      
        LD      DE,C00*256+C10
SCLOOP  PUSH    HL
        PUSH    DE
        LD      A,(ONOFF)       ; Note macros take care of quadrant for HL
        CP      D
        JP      Z,ANDMEM
        CP      E
        JP      Z,ANDMEM
        BLT     ORMEM           ; Generates 256 bytes of instructions
        JP      NXTSC2
ANDMEM  BLT     ANDMEM
NXTSC2  POP     DE
        POP     HL
        LD      A,C01
        CP      E
        JR      Z,PNUMO
        LD      A,-8            ; Restart character
        ADD     A,C
        MOV     C,A
        LD      A,2048/256
        ADD     A,H
        LD      H,A
        LD      DE,C00*256+C01
        JP      SCLOOP
PNUMO   POP     BC
        LD      A,6
        ADD     B
        LD      B,A
        RET
        
; THERE ARE TO BE THE ONLY COMMENTS IN THIS PROGRAM
; 
;	EACH FRIENDLY, ENEMY AND SMART MISSILE'S ARE STORED IN THE
; PAGE 53 FOLLOWS
;	FOLLOWING FORMAT:
;	
;	 +0 --> CURRENT X-POSITION
;	 +1 -->    "    Y-   "
;	 +2 --> ALL THESE ARE
;	 +3 --> USED BY THE
;	 +4 --> LINE DRAWER
;	 +5 --> X-INC
;	 +6 --> Y-INC
;	 +7 --> X-START POSITION
;	 +8 --> Y-START POSITION
;	 +9 --> X-END POSITION
;	+10 --> Y  "     "
;	+11 --> SPEED OF MISSILE
;	+12 --> TARGET TYPE, -1 IS NO MISSILE
;				TARGET'S ARE CODED AS FOLLOWS
;				 ALL ARE MULTIPLES OF TWO ...
;				  0-10 --> A CITY
;				  12-16 --> A MISSILE BASE
;				  ANY OTHER IS MID-AIR DETONATION
DAZSCN	EQU	$/4096*4096+4096
	LIST	ON
ZZZZZZ	EQU	START
PROG	EQU	$
DSAV	EQU	(PROG-SHIFT+0FFH)/256
	LSTAT
	ENTRY	MISSIL
MISSIL	LD	DE,100H
	LD	HL,SHIFT
	LD	BC,PROG-SHIFT
	LDIR
	LD	C,0
	CALL	5
	ORG	DAZSCN
SC1	DS	2048
SC2	DS	2048
	ORG	200H
MISSCT	DS	1
SMISCT	DS	1
EMISCT	DS	1
EXPLCT	DS	1
NMIS	DS	1
NSHP	DS	1
NSMS	DS	1
MSPD	DS	1
MISSTB	DS	13*8
EMISTB	DS	13*8
SMISTB	DS	13*3
EXPLTB	DS	NUMEXP*3
SITEOY	DS	1
SITEOX	DS	1
STRCOL	DS	1
COUNT	DS	1
FM1	DS	1
FM	DS	1
EM1	DS	1
EM	DS	1
BG1	DS	1
BG	DS	1
EX1	DS	1
EX	DS	1
SITEY	DS	1
SITEX	DS	1
T81	DS	1
SPACE	DS	4       ; BOMBER/SATELLITE CONTROL BLOCK X,Y,
; PAGE 54 FOLLOWS
TSC	DS	7
BCORE	DS	7
POINTS	DS	35
PCB	EQU	$
CITYS	DS	1
CITYB	DS	1
CITTAB	DS	6
SCORE	DS	7
BONIS	DS	1
BCHECK	DS	1
SET	DS	1
BASES	DS	3
TIMES	DS	1
COLOR	DS	1
MISFCB	DS	33
HIGHS	DS	128
HIGHS1	DS	128
PLAY1	DS	18
PLAY2	DS	18
PLAYER	DS	1
CCT	DS	1
CST	DS	1
LCTN	DS	1
LSTN	DS	1
TRCT	DS	9
ZEROB   DS      4*3*8*NUMZER
	ORG	$/256*256+256
	DS	64
IIIIII	DS	32
	ORG	$/256*256+256
CRATF	DS	128
	LIST	ON
MAXLOW	EQU	$
	END	MISSIL
